//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						Painbearer of Ilmater - Fighter/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAIlmaFC~
    ~RAIlmaFC 0 0 0 0 1 1 0 1~
    ~RAIlmaFC 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAIlmaFC 3 3 14 3 12 3~
	~RAIlmaFC 0 0 0 0 0 0~
	~RAIlmaFC 3 3 3 3 12 3~
    ~RAIlmaFC 3 3 14 3 12 3~
	~RAIlmaFC 1 0 0 0 0 0 0 0 0~
	~RAIlmaFC 0 0 0 0 0 0~
	~override/RAIlmaFC.2da~
	~K_FC_H   K_FC_HE   K_FC_E   K_FC_G   K_FC_HO   K_FC_HL   K_FC_D~
	~0x00000000 8~
	~FC0~
	~* WAFLAIL HELM04 * RING31 MISC72 OHRCLCK3 BOOT01 AMUL27 BRAC16 BELT09 * * * * * * STAF10 HAMM09 *~
	SAY @1301
	SAY @1302
	SAY @1310

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAIlmaFC~
		clascolr = ~14 28 222 9 14~
		clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAIlmaFC~
      kit_clab = ~RAIlmaFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun/Lib/Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAIlmaFC%	classID = 8 STR_VAR	kitname = ~RAILMA~  END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAFILMA.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAFCILMA~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAFCILMA.itm~ SAY NAME1 @1304 SAY NAME2 @1304 SAY DESC @1305
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAIlmaFC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~ILMAS~
WRITE_ASCII 0x058 ~ILMAL~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAIlmaFC + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	~%animation%~ WITH
				~4A~ ~3A~ ~2A~ BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAIlmaFC~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//EoF