//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Waveservant of Umberlee
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAUmbe~
    ~RAUmbe 0 1 0 1 1 1 0 1~
    ~RAUmbe 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 1 1 1 1 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAUmbe 3 3 15 3 13 3~
	~RAUmbe 0 0 0 0 0 0~
	~RAUmbe 3 3 15 3 13 3~
    ~RAUmbe 3 3 15 3 13 3~
	~RAUmbe 0 0 0 0 0 0 0 0 1~
	~RAUmbe 0 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RAUmbe.2da~
	~K_C_H K_C_HO K_C_HE~
	~0x04000000 3~
	~Cl0~
	~LEAT19 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * WASPEAR HAMM09 *~
	SAY @1901
	SAY @1902
	SAY @1903

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAUmbe~
		clascolr = ~81 121 55 10 10~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 0 1 0 0 0 1~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 1
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 1
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~minor~
	s_Benediction 	= ~x~
	s_Destruction 	= ~major~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~major~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~minor~
	s_Dread 		= ~minor~
	s_Vigor 		= ~x~
	s_Affliction 	= ~major~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~major~
	s_Air 			= ~major~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY_EXISTING ~SPPR403.spl~ ~override/RAUMBEF.spl~
WRITE_SHORT 0x1C 4                                                    
WRITE_LONG  0x34 1                                                         
WRITE_LONG  0x22 00
LPF ALTER_SPELL_HEADER INT_VAR projectile = 0 target = 5 range = 1 speed = 0 location = 0 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 9   END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 50  END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 139 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 141 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 215 END
LPF ALTER_EFFECT INT_VAR target = 2 timing = 9 duration = 0 resist_dispel = 0 END

COPY "DeitiesOfFaerun\Spl\RAHSUMBE.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSUMBE.itm" override SAY NAME1 @1913 SAY NAME2 @1913 SAY DESC @1914
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAUmbe + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END
		  
ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN

COPY ~DeitiesOfFaerun\Spl\Import\Umberlee\Complement~ override

ADD_PROJECTILE ~DeitiesOfFaerun\Spl\Import\Umberlee\Complement\RASWAVE.PRO~

COPY ~DeitiesOfFaerun\Spl\Import\Umberlee\RAPR426.SPL~ ~override/RAUMBE1.SPL~ SAY NAME1 @1904 SAY UNIDENTIFIED_DESC @1905
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RASWAVE% END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 35568 match_opcode = 139 parameter1 = EVAL "%RA_Stunned%" END
LPM SPELL_TO_INNATE

END

ACTION_IF GAME_IS ~iwdee~ BEGIN

COPY ~DeitiesOfFaerun\Spl\Import\Umberlee\Complement\RAWI533b.BAM~ override

COPY ~DeitiesOfFaerun\Spl\Import\Umberlee\Complement\RAWI533c.BAM~ override

COPY_EXISTING ~SPPR426.SPL~ ~override/RAUMBE1.SPL~
LPM SPELL_TO_INNATE

END

COPY ~DeitiesOfFaerun\Spl\RAUMBE2.SPL~ override SAY NAME1 @1906 SAY UNIDENTIFIED_DESC @1907

COPY ~DeitiesOfFaerun\Spl\RAUMBE3.SPL~ override SAY NAME1 @1908 SAY UNIDENTIFIED_DESC @1909		  

COPY "DeitiesOfFaerun\Spl\RAUMBE4.spl" override SAY NAME1 @1910 SAY UNIDENTIFIED_DESC @1911

COPY "DeitiesOfFaerun\Spl\RAUMB4E1.eff" override

COPY "DeitiesOfFaerun\Spl\RAUMB4E2.eff" override

COPY "DeitiesOfFaerun\Spl\RAUMB4E3.eff" override

COPY "DeitiesOfFaerun\Cre\WaterElemental\Complement" override                                    //Water Elemental - animation, bam, sounds, itm

COPY "DeitiesOfFaerun\Cre\WaterElemental\RAWatEl1.cre" override SAY NAME1 @1912 SAY NAME2 @1912  //Water Elemental - creature 12 HD

COPY "DeitiesOfFaerun\Cre\WaterElemental\RAWatEl2.cre" override SAY NAME1 @1912 SAY NAME2 @1912  //Water Elemental - creature 16 HD

COPY "DeitiesOfFaerun\Cre\WaterElemental\RAWatEl3.cre" override SAY NAME1 @1912 SAY NAME2 @1912  //Water Elemental - creature 24 HD

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAUmbe + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@1902)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUmbe~) parameter2 = 9 special = RESOLVE_STR_REF (@1902) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUmbe~) parameter2 = 9 special = RESOLVE_STR_REF (@1902) END
	END
END
END
BUT_ONLY

ACTION_FOR_EACH file IN ~UMBES~ ~UMBEL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~UMBEDEHY~ ~UMBEDROW~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF