//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					    Holy Champion of Torm - Fighter/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATormFC~
    ~RATormFC 0 0 0 0 1 1 0 1~
    ~RATormFC 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATormFC 3 3 12 3 15 3~
	~RATormFC 0 0 0 0 0 0~
	~RATormFC 3 3 12 3 15 3~
    ~RATormFC 3 3 12 3 15 3~
	~RATormFC 1 0 0 0 0 0 0 0 0~
	~RATormFC 0 0 0 0 0 0~
	~override\RATormFC.2da~
	~K_FC_H K_FC_HE K_FC_D K_FC_HO~
	~0x00000000 8~
	~FC0~
	~PLAT18 SHLD21 HELM21 * RING09 RING31 CLCK05 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * BLUN14 HAMM09 *~
	SAY @1501
	SAY @1502
	SAY @1506

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATormFC~
		clascolr = ~66 140 190 164 186~
		clswpbon = ~1 0 2~
		clsrcreq = ~1 0 1 1 0 0 1~
		hpclass = ~HPWAR~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RATormFC~
      kit_clab = ~RATormFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RATormFC%	classID = 8 STR_VAR	kitname = ~RATORM~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAFTORM.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAFCTORM~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAFCTORM.itm~ SAY NAME1 @1504 SAY NAME2 @1504 SAY DESC @1505
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RATormFC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~TORMS~
WRITE_ASCII 0x058 ~TORML~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATormFC + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//EoF