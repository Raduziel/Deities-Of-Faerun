//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						        Nightcloak of Shar
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAShar~
    ~RAShar 0 0 0 0 1 1 0 0~
    ~RAShar 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAShar 14 3 3 3 12 3~
	~RAShar 0 0 0 0 0 0~
	~RAShar 14 3 3 3 12 3~
    ~RAShar 14 3 3 3 12 3~
	~RAShar 0 0 0 0 0 1 0 0 0~
	~RAShar 1 1 1 1 1 1~
	~DeitiesOfFaerun\Tables\RAShar.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HO   K_C_HL   K_C_D~
	~0x04000000 3~
	~Cl0~
	~CHAN18 SHLD20 HELM04 * RING05 RING39 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 WAFLAIL *~
	SAY @401
	SAY @402
	SAY @403

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAShar~
		clascolr = ~205 136 118 41 104~
		clswpbon = ~0 0 3~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~x~
	s_Death 		= ~x~
	s_Benediction 	= ~minor~
	s_Destruction 	= ~x~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~major~
	s_Thought 		= ~major~
	s_Dread 		= ~major~
	s_Vigor 		= ~x~
	s_Affliction 	= ~minor~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~major~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~major~
	s_Magic 		= ~minor~
	s_Perdition     = ~minor~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RASHARFE.spl" override

COPY "DeitiesOfFaerun\Spl\RASHARFE.eff" override

COPY "DeitiesOfFaerun\Spl\RASHAREF.spl" override

COPY "DeitiesOfFaerun\Spl\RAHSSHAR.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSSHAR.itm" override SAY NAME1 @414 SAY NAME2 @414 SAY DESC @415
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAShar + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END


ADD_PROJECTILE ~DeitiesOfFaerun\Spl\RASHAR1.PRO~
		  
COPY "DeitiesOfFaerun\Spl\RASHAR1.spl" override  SAY NAME1 @404 SAY  UNIDENTIFIED_DESC @405 //Darkness
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RASHAR1% END

COPY_EXISTING ~SPWI106.spl~ ~override/RASHAR2.spl~                                          //Blindness
LPM SPELL_TO_INNATE

COPY "DeitiesOfFaerun\Spl\RASHAR3.spl" override  SAY NAME1 @406 SAY UNIDENTIFIED_DESC @407  //Nightmare

COPY_EXISTING ~SPWI505.spl~ ~override/RASHAR4.spl~                                          //Shadow Door
LPM SPELL_TO_INNATE

COPY "DeitiesOfFaerun\Spl\RASHAR5.vvc" override

COPY "DeitiesOfFaerun\Spl\RASHAR5.spl" override  SAY NAME1 @408 SAY UNIDENTIFIED_DESC @409  //Eyebite

COPY "DeitiesOfFaerun\Spl\RASHARE1.spl" override SAY NAME1 @410 SAY UNIDENTIFIED_DESC @410  //Charm Gaze
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_Dominated%" END

COPY "DeitiesOfFaerun\Spl\RASHARE2.spl" override SAY NAME1 @411 SAY UNIDENTIFIED_DESC @411  //Fear Gaze
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_Panic%" END

COPY "DeitiesOfFaerun\Spl\RASHARE3.spl" override SAY NAME1 @412 SAY UNIDENTIFIED_DESC @412  //Sicken Gaze
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_FoulDisease%" END

COPY "DeitiesOfFaerun\Spl\RASHARE4.spl" override SAY NAME1 @413 SAY UNIDENTIFIED_DESC @413  //Sleep Gaze
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_Sleep%" END

COPY "DeitiesOfFaerun\Tables\RASHAR5.2da" override

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAShar + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@402)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAShar~) parameter2 = 9 special = RESOLVE_STR_REF (@402) END
	END
END
END
BUT_ONLY

ACTION_FOR_EACH file IN ~SHARS~ ~SHARL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SHARDARK~ ~SHAREYEB~ ~SHARNIGH~ ~RASHARE1~ ~RASHARE2~ ~RASHARE3~ ~RASHARE4~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF