//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Strifeleader of Cyric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RACyri~
    ~RACyri 1 0 0 0 1 1 0 1~
    ~RACyri 1 0 0 0 1 1 0 1 0 1 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RACyri 13 3 3 3 13 3~
	~RACyri 0 0 0 0 0 0~
	~RACyri 13 3 3 3 13 3~
    ~RACyri 13 3 3 3 13 3~
	~RACyri 0 0 0 0 0 1 0 0 1~
	~RACyri 0 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RACyri.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HO   K_C_HL   K_C_D~
	~0x04000000 3~
	~Cl0~
	~PLAT18 SHLD21 HELM03 * RING22 RING39 CLCK07 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 HAMM09 *~
	SAY @301
	SAY @302
	SAY @303

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RACyri~
		clascolr = ~99 140 60 62 61~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 1
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~x~
	s_Death 		= ~major~
	s_Benediction 	= ~x~
	s_Destruction 	= ~minor~
	s_Protection 	= ~minor~
	s_War 			= ~x~
	s_Exploration   = ~minor~
	s_Knowledge 	= ~minor~
	s_Deception 	= ~major~
	s_Thought 		= ~major~
	s_Dread 		= ~major~
	s_Vigor 		= ~x~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~major~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RACYRIF.spl" override

COPY "DeitiesOfFaerun\Spl\RAHSCYRI.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSCYRI.itm" override SAY NAME1 @308 SAY NAME2 @308 SAY DESC @309
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RACyri + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY "DeitiesOfFaerun\Spl\RACYRI1.spl" override SAY NAME1 @304 SAY UNIDENTIFIED_DESC @305 //Dark Aura

COPY "DeitiesOfFaerun\Spl\RACYRIE.spl" override                                           //Bonuses for Evil-aligned

COPY "DeitiesOfFaerun\Spl\RACYRIG.spl" override                                           //Penalties for Good-aligned

COPY "DeitiesOfFaerun\Spl\RACYRI2.spl" override SAY NAME1 @306 SAY UNIDENTIFIED_DESC @307 //Instill Madness
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@310) END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RACyri + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@302)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~CYRIS~ ~CYRIL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~CYRIDAAU~ ~CYRIINMA~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF