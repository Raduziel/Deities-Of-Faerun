//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Malagent of Talona
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATlna~
    ~RATlna 0 0 0 0 1 1 0 1~
    ~RATlna 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATlna 3 3 14 3 14 3~
	~RATlna 0 0 0 0 0 0~
	~RATlna 3 3 14 3 14 3~
    ~RATlna 3 3 14 3 14 3~
	~RATlna 0 0 0 0 0 0 0 0 1~
	~RATlna 1 1 1 1 1 1~
	~DeitiesOfFaerun\Tables\RATlna.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HL   K_C_D   K_C_HO~
	~0x04000000 3~
	~Cl0~
	~LEAT18 * HELM07 * RING46 RING39 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN22 HAMM09 *~
	SAY @2101
	SAY @2102
	SAY @2103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATlna~
		clascolr = ~20 51 37 95 20~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~major~
	s_Benediction 	= ~x~
	s_Destruction 	= ~minor~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~minor~
	s_Thought 		= ~major~
	s_Dread 		= ~major~
	s_Vigor 		= ~minor~
	s_Affliction 	= ~major~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~major~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAHSTLNA.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSTLNA.itm" override SAY NAME1 @2108 SAY NAME2 @2108 SAY DESC @2109
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATlna + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY "DeitiesOfFaerun\Spl\RATLNAFE.spl" override		  
		  
COPY_EXISTING ~SPCL423.spl~ ~override/RATLNA1.spl~
LPM SPELL_TO_INNATE

COPY "DeitiesOfFaerun\Spl\RATLNA2.spl" override SAY NAME1 @2104 SAY UNIDENTIFIED_DESC @2105

COPY "DeitiesOfFaerun\Spl\RATLNA21.spl" override

COPY "DeitiesOfFaerun\Spl\RATLNA22.spl" override

COPY_EXISTING ~SPWI409.spl~ ~override/RATLNA3.spl~
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR411.spl~ ~override/RATLNA4.spl~
LPM SPELL_TO_INNATE

ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN

COPY ~DeitiesOfFaerun\Spl\Import\Talona\Complement~ override

ADD_PROJECTILE ~DeitiesOfFaerun\Spl\Import\Talona\Complement\RATLNA.PRO~

COPY ~DeitiesOfFaerun\Spl\Import\Talona\RAPR423.spl~ ~override\RATLNA5.spl~ SAY NAME1 @2106 SAY UNIDENTIFIED_DESC @2107
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_Blinded%" END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RATLNA% END
LPM SPELL_TO_INNATE

END

ACTION_IF GAME_IS ~iwdee~ BEGIN

COPY_EXISTING ~SPPR423.spl~ ~override\RATLNA5.spl~
LPM SPELL_TO_INNATE

END


COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATlna + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@2102)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~TLNAS~ ~TLNAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~TLNAPOTO~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF