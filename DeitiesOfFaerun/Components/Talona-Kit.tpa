//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Malagent of Talona
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATlna~
    ~RATlna 0 0 0 0 1 1 0 1~
    ~RATlna 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATlna 3 3 14 3 14 3~
	~RATlna 0 0 0 0 0 0~
	~RATlna 3 3 14 3 14 3~
    ~RATlna 3 3 14 3 14 3~
	~RATlna 0 0 0 0 0 0 0 0 1~
	~RATlna 0 0 1 1 0 0~
	~DeitiesOfFaerun\Tables\RATlna.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HL   K_C_D   K_C_HO~
	~0x00000000 3~
	~Cl0~
	~LEAT18 * HELM07 * RING46 RING39 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN22 HAMM09 *~
	SAY @2101
	SAY @2102
	SAY @2103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATlna~
		clascolr = ~20 54 39 92 137~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RATLNA~  clabfile = ~RATLNA~	END

END

COPY "DeitiesOfFaerun\Spl\RAHSTLNA.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSTLNA.itm" override SAY NAME1 @2106 SAY NAME2 @2106 SAY DESC @2107
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATlna + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY "DeitiesOfFaerun\Spl\RATLNAF.spl" override		  

//Poison Weapon
		  
ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]ASSASSIN_POISON[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~ASSASSIN_POISON~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATLNA1.spl~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATLNA1~ END

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATLNA1~ END

END

COPY "DeitiesOfFaerun\Spl\RATLNA2.spl" override SAY NAME1 @2104 SAY UNIDENTIFIED_DESC @2105

COPY "DeitiesOfFaerun\Spl\RATLNA21.spl" override

COPY "DeitiesOfFaerun\Spl\RATLNA22.spl" override

//Contagion

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_CONTAGION[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_CONTAGION~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATLNA3.spl~
LPM ~SPELL_TO_INNATE~

END

//Poison

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_POISON[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_POISON~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATLNA4.spl~
LPM ~SPELL_TO_INNATE~

END

//Cloud of Pestilence

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CLOUD_OF_PESTILENCE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CLOUD_OF_PESTILENCE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATLNA5.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/CloudofPestilance/IDPRO309.PRO~

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/CloudofPestilance/SPPR423.spl~ 1 4 CLERIC_CLOUD_OF_PESTILENCE
SAY NAME1 @999987 SAY NAME2 @999987 SAY UNIDENTIFIED_DESC @999988 SAY DESC @999988
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO309% END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 37800 parameter1 = EVAL "%RA_Blinded%" END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/CloudofPestilance/SPPR423B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/CloudofPestilance/SPPR423C.BAM~	~override\%spell_res%C.BAM~

COPY_EXISTING	~%spell_res%.spl~	~override/RATLNA5.spl~
LPM ~SPELL_TO_INNATE~

END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATlna + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@2102)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~TLNAS~ ~TLNAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~TLNAPOTO~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF