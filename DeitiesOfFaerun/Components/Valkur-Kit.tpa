//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Wavetamer of Valkur
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAValk~
    ~RAValk 0 0 0 1 1 1 0 1~
    ~RAValk 0 0 0 1 1 1 0 1 0 0 0 0 0 0 1 0 1 1 1 0 1 1 1 0 0 0 0 1 1 0 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAValk 14 3 3 3 10 12~
	~RAValk 0 0 0 0 0 0~
	~RAValk 14 3 3 3 10 12~
    ~RAValk 14 3 3 3 10 12~
	~RAValk 0 0 0 0 0 0 1 0 0~
	~RAValk 1 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAValk.2da~
	~K_C_H K_C_HE K_C_D K_C_HO K_C_E K_C_HL K_C_G~
	~0x00000000 3~
	~Cl0~
	~CHAN18 SW1H30 HELM07 * RING28 RING22 CLCK05 BOOT05 AMUL27 BRAC10 BELT04 * * * * * * SPER07 HAMM09 *~
	SAY @3801
	SAY @3802
	SAY @3803

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAValk~
		clascolr = ~80 58 105 95 57~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAVALK~  clabfile = ~RAVALK~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSVALK.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSVALK~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSVALK.itm~ SAY NAME1 @3804 SAY NAME2 @3804 SAY DESC @3805
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAValk + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~VALKS~
WRITE_ASCII 0x058 ~VALKL~

//Spells

//Goodberry

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_GOODBERRY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_GOODBERRY~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK1.spl~
LPM ~SPELL_TO_INNATE~

END

//Call Lightning

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CALL_LIGHTNING[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CALL_LIGHTNING~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK2.spl~
LPM ~SPELL_TO_INNATE~

END

//Smashing Wave
		  
ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_SMASHING_WAVE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_SMASHING_WAVE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK3.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/SmashingWave/SWAVE.PRO~

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/SmashingWave/SPPR426.spl~ 1 4 CLERIC_SMASHING_WAVE
SAY NAME1 @9999139 SAY NAME2 @9999139 SAY UNIDENTIFIED_DESC @9999140 SAY DESC @9999140
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %SWAVE% END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 35568 parameter1 = EVAL "%RA_Stunned%"     END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 37340 parameter1 = EVAL "%RA_Unconscious%" END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/SmashingWave/SPPR426B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/SmashingWave/SPPR426C.BAM~	~override\%spell_res%C.BAM~

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK3.spl~
LPM ~SPELL_TO_INNATE~

END

//Conjure Water Elemental

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]RADUZIEL_CONJURE_WATER_ELEMENTAL[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~RADUZIEL_CONJURE_WATER_ELEMENTAL~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK4.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

LAF cd_new_summon_table STR_VAR descript = "WATER_ELEMENTAL_RADUZIEL" 2da_file = RAELEMW RET RAELEMW = table END
ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/SPPRRA1.spl~ 1 6 RADUZIEL_CONJURE_WATER_ELEMENTAL
SAY NAME1 @9999141 SAY NAME2 @9999141 SAY UNIDENTIFIED_DESC @9999142 SAY DESC @9999142
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF ALTER_EFFECT	   INT_VAR match_opcode = 331 parameter2 = RAELEMW END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/SPPRRA1B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/SPPRRA1C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/RAELEMW.2DA~	~override~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/RA12WATR.CRE~	~override~
SAY NAME1 @9999143 SAY NAME2 @9999143

COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/RA16WATR.CRE~	~override~
SAY NAME1 @9999143 SAY NAME2 @9999143

COPY ~DeitiesofFaerun/Lib/Spheres/Import/ConjureWaterElemental/RA24WATR.CRE~	~override~

COPY_EXISTING	~%spell_res%.spl~	~override/RAVALK4.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

COPY_EXISTING ~RAValk.2da~ ~override/RAValkFC.2da~
SET_2DA_ENTRY 5 25 50 ~AP_RAFVALK~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 96 99 103 104 105 106 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMyrk + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"AX" "BW" "CB" "DD" "HB" "S0" "S1" "S2" "S3" "SP" "SS" "4A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAValk~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~VALKS~ ~VALKL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF