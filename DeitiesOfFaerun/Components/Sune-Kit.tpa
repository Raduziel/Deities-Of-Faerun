//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Heartwarder of Sune
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RASune~
    ~RASune 0 0 0 0 1 1 0 1~
    ~RASune 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RASune 3 3 3 3 12 16~
	~RASune 0 0 0 0 0 0~
	~RASune 3 3 3 3 12 16~
    ~RASune 3 3 3 3 12 16~
	~RASune 0 0 0 0 0 0 1 0 0~
	~RASune 1 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RASune.2da~
	~K_C_H   K_C_HE   K_C_E~
	~0x00000000 3~
	~Cl0~
	~PLAT18 SHLD21 HELM03 * RING22 RING39 CLCK07 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 HAMM09 *~
	SAY @101
	SAY @102
	SAY @103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RASune~
		clascolr = ~67 81 47 46 70~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 0 0 0 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RASUNE~  clabfile = ~RASUNE~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSSUNE.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSSUNE~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSSUNE.itm~ SAY NAME1 @104 SAY NAME2 @104 SAY DESC @105
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RASune + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~SUNES~
WRITE_ASCII 0x058 ~SUNEL~

//Spells

COPY "DeitiesOfFaerun\Spl\RASUNEF.spl" override

COPY "DeitiesOfFaerun\Spl\RASUNE1.spl"  override SAY NAME1 @106 SAY UNIDENTIFIED_DESC @107                  //Rapture

COPY "DeitiesOfFaerun\Spl\RASUNEA.spl"  override                                                            //Effect upon allies
LPF ALTER_EFFECT INT_VAR match_parameter2 = 999 match_opcode = 240 parameter2 = EVAL "%RA_DeafIc%" END
LPF ALTER_EFFECT INT_VAR match_parameter2 = 998 match_opcode = 240 parameter2 = EVAL "%RA_FeebIc%" END
LPF ALTER_EFFECT INT_VAR match_parameter2 = 997 match_opcode = 240 parameter2 = EVAL "%RA_NausIc%" END
LPF ALTER_EFFECT INT_VAR match_parameter2 = 996 match_opcode = 240 parameter2 = EVAL "%RA_FatiIc%" END

COPY "DeitiesOfFaerun\Spl\RASUNEE.spl"  override                                                            //Effect upon enemies
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 6; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 5; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 5) match_duration = 6 duration = (6 * i) END
	END

//Flame Strike

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_FLAME_STRIKE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_FLAME_STRIKE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASUNE2.spl~
LPM ~SPELL_TO_INNATE~

END

//Dire Charm

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_DIRE_CHARM[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_DIRE_CHARM~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASUNE3.spl~
LPF	CLONE_EFFECT INT_VAR	match_opcode = 324 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RASUNE3~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR	match_opcode = 324 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RASUNE3~	END

END

COPY "DeitiesOfFaerun\Spl\RASUNE4.spl"  override SAY NAME1 @108 SAY UNIDENTIFIED_DESC @109                   //Emotion

COPY "DeitiesOfFaerun\Tables\RASUNE4.2da" override                                                           //Emotion 2da

//Fear

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_EMOTION_FEAR[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_EMOTION_FEAR~	RET	spell_res	END

APPEND ~RASune4.2da~ ~FEAR %spell_res% 3~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/EmotionFear/IDPRO255.PRO~

ADD_SPELL ~DeitiesOfFaerun/Lib/Spheres/Import/EmotionFear/SPWI428.spl~ 2 4 WIZARD_EMOTION_FEAR
SAY NAME1 @9999112 SAY NAME2 @9999112 SAY UNIDENTIFIED_DESC @9999113 SAY DESC @9999113
TEXT_SPRINT	 spell_res	~%DEST_RES%~	TO_UPPER	spell_res
TEXT_SPRINT	 $res_of_label(~WIZARD_EMOTION_FEAR~) ~%spell_res%~
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO255% END
LPF	ALTER_EFFECT       STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 35484 parameter1 = EVAL "%RA_Panic%" END
LPF ALTER_EFFECT       INT_VAR match_opcode = 321 STR_VAR match_resource = ~SPWI427~ resource = ~XXWI427~ END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionFear/SPWI428B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionFear/SPWI428C.BAM~	~override\%spell_res%C.BAM~

APPEND ~RASune4.2da~ ~FEAR %spell_res% 3~

END

//Courage

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_EMOTION_COURAGE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_EMOTION_COURAGE~	RET	spell_res	END

APPEND ~RASune4.2da~ ~COURAGE %spell_res% 3~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/EmotionCourage/IDPRO407.PRO~

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@9999116) STR_VAR bam_file = RACOURAG RET iconcour = icon END
ADD_SPELL ~DeitiesOfFaerun/Lib/Spheres/Import/EmotionCourage/SPWI427.spl~ 2 4 WIZARD_EMOTION_COURAGE
SAY NAME1 @9999114 SAY NAME2 @9999114 SAY UNIDENTIFIED_DESC @9999115 SAY DESC @9999115
TEXT_SPRINT	 spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO407% END
LPF	ALTER_EFFECT       STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF ALTER_EFFECT  	   INT_VAR match_opcode = 142 match_parameter2 = 187 parameter2 = iconcour END
LPF	ALTER_EFFECT	   INT_VAR match_opcode = 321 STR_VAR match_resource = ~SPWI428~ resource = EVAL $res_of_label(~WIZARD_EMOTION_FEAR~)	END
LPF	DELETE_EFFECT	   STR_VAR match_resource = ~SPIN115~	END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionCourage/SPWI427B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionCourage/SPWI427C.BAM~	~override\%spell_res%C.BAM~

COPY_EXISTING ~%res_of_label_WIZARD_EMOTION_FEAR%.spl~ override
LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = ~XXWI427~ resource = EVAL ~%spell_res%~ END

APPEND ~RASune4.2da~ ~COURAGE %spell_res% 3~

END

//Hopelessness

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/EmotionHopelessness/IDPRO255.PRO~

COPY ~DeitiesOfFaerun/Lib/Spheres/Import/EmotionHopelessness/SPWI411.spl~ override
SAY NAME1 @9999119 SAY NAME2 @9999119 SAY UNIDENTIFIED_DESC @9999120 SAY DESC @9999120
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO255% END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 1280  parameter1 = EVAL "%RA_Stunned%"  END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 14980 parameter1 = EVAL "%RA_Hopeless%" END
LPF ALTER_EFFECT       INT_VAR match_opcode = 321 STR_VAR match_resource = ~SPWI429~ resource = ~XXWI429~    END

APPEND ~RASune4.2da~ ~HOPELESSNESS SPWI411 3~

//Hope

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_EMOTION_HOPE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_EMOTION_HOPE~	RET	spell_res	END

APPEND ~RASune4.2da~ ~HOPE %spell_res% 3~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/EmotionHope/IDPRO407.PRO~

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@9999123) STR_VAR bam_file = RAHOPEIC RET iconhope = icon END
ADD_SPELL ~DeitiesOfFaerun/Lib/Spheres/Import/EmotionHope/SPWI429.spl~ 2 4 WIZARD_EMOTION_HOPE
SAY NAME1 @9999121 SAY NAME2 @9999121 SAY UNIDENTIFIED_DESC @9999122 SAY DESC @9999122
TEXT_SPRINT	 spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO407% END
LPF	ALTER_EFFECT       STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF ALTER_EFFECT  	   INT_VAR match_opcode = 142 match_parameter2 = 186 parameter2 = iconhope END
LPF	ALTER_EFFECT	   INT_VAR match_opcode = 321 STR_VAR match_resource = ~SPPR716~ resource = EVAL $res_of_label(~CLERIC_SYMBOL_OF_HOPELESSNESS~)	END
LPF	DELETE_EFFECT	   STR_VAR match_resource = ~SPIN115~	END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionHope/SPWI429B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/EmotionHope/SPWI429C.BAM~	~override\%spell_res%C.BAM~

COPY_EXISTING ~SPWI411.spl~ override
LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR match_resource = ~XXWI429~ resource = EVAL ~%spell_res%~ END

APPEND ~RASune4.2da~ ~HOPE %spell_res% 3~

END

COPY_EXISTING ~RASUNE4.2da~ ~override~
PRETTY_PRINT_2DA

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RASune + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~SUNES~ ~SUNEL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SUNERAPT~ ~SUNEEMOT~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF