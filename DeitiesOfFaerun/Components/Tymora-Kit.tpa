//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Luckbringer of Tymora
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATymo~
    ~RATymo 0 0 0 0 1 1 0 1~
    ~RATymo 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATymo 3 14 3 3 15 3~
	~RATymo 0 0 0 0 0 0~
	~RATymo 3 14 3 3 15 3~
    ~RATymo 3 14 3 3 15 3~
	~RATymo 0 0 0 0 0 0 1 0 0~
	~RATymo 0 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RATymo.2da~
	~K_C_H   K_C_HE   K_C_HL~
	~0x00000000 3~
	~Cl0~
	~PLAT16 SHLD22 HELM04 * RING31 RING09 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 HAMM09 *~
	SAY @3401
	SAY @3402
	SAY @3403

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATymo~
		clascolr = ~14 96 97 5 17~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 0 1 0 1 0 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RATYMO~  clabfile = ~RATYMO~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSTYMO.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSTYMO~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSTYMO.itm~ SAY NAME1 @3404 SAY NAME2 @3404 SAY DESC @3405
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RATymo + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~TYMOS~
WRITE_ASCII 0x058 ~TYMOL~

//Spells

//Luck
		  
ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_LUCK[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_LUCK~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATYMO1.spl~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATYMO1~ END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATYMO1~ END

END		  

COPY "DeitiesOfFaerun\Spl\RATYMO2.spl"  override SAY NAME1 @3406 SAY UNIDENTIFIED_DESC @3407	             //Moment
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 4; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 3; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 3) match_duration = 6 duration = (6 * i) END
	END

COPY "DeitiesOfFaerun\Spl\RATYMO3.spl"  override SAY NAME1 @3408 SAY UNIDENTIFIED_DESC @3409	             //Favor of Tymora

//Lower Resistance

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_LOWER_RESISTANCE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_LOWER_RESISTANCE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATYMO4.spl~
LPM ~SPELL_TO_INNATE~

END	

COPY "DeitiesOfFaerun\Spl\RATYMO5.spl"  override SAY NAME1 @3410 SAY UNIDENTIFIED_DESC @3411	             //Luckbolt

COPY "DeitiesOfFaerun\Spl\RATymo51.spl"  override

COPY "DeitiesOfFaerun\Spl\RATymo52.spl"  override

COPY "DeitiesOfFaerun\Spl\RATymo53.spl"  override

COPY "DeitiesOfFaerun\Spl\RATYMO6.spl"  override SAY NAME1 @3412 SAY UNIDENTIFIED_DESC @3413	             //Tymora's Blessing

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RATymo + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~TYMOS~ ~TYMOL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~TYMOBLES~ ~TYMOFAVO~ ~TYMOLUCK~ ~TYMOMOME~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF