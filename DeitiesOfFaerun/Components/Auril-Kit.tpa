//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Icevassal of Auril
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAAuri~
    ~RAAuri 0 0 0 0 1 1 0 1~
    ~RAAuri 0 0 0 0 1 1 0 1 0 0 0 1 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAAuri 3 3 13 3 12 3~
	~RAAuri 0 0 0 0 0 0~
	~RAAuri 3 3 13 3 12 3~
    ~RAAuri 3 3 13 3 12 3~
	~RAAuri 0 0 0 0 0 1 0 0 0~
	~RAAuri 0 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RAAuri.2da~
	~K_C_H K_C_HO K_C_HE K_C_D~
	~0x00000000 3~
	~Cl0~
	~CHAN18 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 HAMM09 *~
	SAY @1701
	SAY @1702
	SAY @1703

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAAuri~
		clascolr = ~148 17 105 222 97~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 0 1 1 0 0 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAAURI~  clabfile = ~RAAURI~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSAURI.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSAURI~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSAURI.itm~ SAY NAME1 @1704 SAY NAME2 @1704 SAY DESC @1705
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAAuri + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~AURIS~
WRITE_ASCII 0x058 ~AURIL~

//Spells

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_PROTECTION_FROM_COLD[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_PROTECTION_FROM_COLD~	RET	spell_res	END

COPY_EXISTING ~%spell_res%.spl~ ~override/RAAURIF.spl~
WRITE_SHORT 0x1C 4                                                    
WRITE_LONG  0x34 1                                                         
WRITE_LONG  0x22 00
FOR (level = 2; level < 51; level =+ 1) BEGIN
LPF DELETE_SPELL_HEADER INT_VAR silent = 1 header_type = ~-1~ min_level = level  END
END
LPF ALTER_SPELL_HEADER INT_VAR projectile = 0 target = 5 range = 1 speed = 0 location = 0 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 9   END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 50  END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 139 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 141 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 215 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 321 END
LPF ALTER_EFFECT INT_VAR target = 2 timing = 9 duration = 0 resist_dispel = 0 END

END

ADD_PROJECTILE ~DeitiesOfFaerun\Spl\RAAURI1.PRO~

COPY "DeitiesOfFaerun\Spl\RAAURI1.spl" override SAY NAME1 @1706 SAY UNIDENTIFIED_DESC @1707    //Frost Dagger
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RAAURI1% END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@1708) END

COPY "DeitiesOfFaerun\Spl\RAAURI2.spl" override SAY NAME1 @1709 SAY UNIDENTIFIED_DESC @1710    //Frost Whip

COPY "DeitiesOfFaerun\Spl\RAAURI3.spl" override SAY NAME1 @1711 SAY UNIDENTIFIED_DESC @1712    //Frost Sword
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 6; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 5; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 5) match_duration = 6 duration = (6 * i) END
	END

COPY "DeitiesOfFaerun\Itm\IceBlade\AURIBLAD.itm" override SAY NAME1 @1711 SAY NAME2 @1711      //Frost Sword (Itm)

COPY "DeitiesOfFaerun\Itm\IceBlade\AURIBLAD.bam" override                                      //Frost Sword (Bam)
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun\Itm\IceBlade\MOS%original_base_index%.pvrz~
    END
  END

//Ice Storm

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_ICE_STORM[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_ICE_STORM~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAAURI4.spl~
LPM ~SPELL_TO_INNATE~

END  

//Cone of Cold

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_CONE_OF_COLD[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_CONE_OF_COLD~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAAURI5.spl~
LPM ~SPELL_TO_INNATE~

END

//Conjure Ice Elemental

LAF cd_new_summon_table STR_VAR descript = "ICE_ELEMENTAL_RADUZIEL" 2da_file = RAELEMI RET RAELEMI = table END  
COPY "DeitiesOfFaerun\Spl\RAAURI6.spl" override SAY NAME1 @1713 SAY UNIDENTIFIED_DESC @1714
LPF ALTER_EFFECT	   INT_VAR match_opcode = 331 parameter2 = RAELEMI END
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 13; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 12; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 12) match_duration = 60 duration = (60 * i) END
	END

COPY_EXISTING ~smtables.2da~ ~override~
PRETTY_PRINT_2DA

COPY "DeitiesOfFaerun\Cre\IceElemental\Complement"  override                                  //Ice Elemental - animation, bam, sounds, itm

COPY "DeitiesOfFaerun\Cre\IceElemental\RAELEMI.2DA" override                                  //Ice Elemental - 2da

COPY "DeitiesOfFaerun\Cre\IceElemental\RA12ice.cre" override SAY NAME1 @1715 SAY NAME2 @1715  //Ice Elemental - creature 12 HD

COPY "DeitiesOfFaerun\Cre\IceElemental\RA16ice.cre" override SAY NAME1 @1715 SAY NAME2 @1715  //Ice Elemental - creature 16 HD

COPY "DeitiesOfFaerun\Cre\IceElemental\RA24ice.cre" override SAY NAME1 @1715 SAY NAME2 @1715  //Ice Elemental - creature 24 HD

//Multiclass CLAB

COPY_EXISTING ~RAAuri.2da~ ~override/RAAuriCM.2da~
SET_2DA_ENTRY 8 25 50 ~AP_RAMAURI~
SET_2DA_ENTRY 8  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAAuri + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAuri~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~AURIS~ ~AURIL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~AURIDAGG~ ~AURIELEM~ ~AURIICBL~ ~AURIWHIP~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF