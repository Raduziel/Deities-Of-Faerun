//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							YESLICK, THE ALAGHAR OF CLANGEDDIN
//__________________________________________________________________________________
//__________________________________________________________________________________

INCLUDE ~DeitiesOfFaerun\Lib\Multiclass\qd_multiclass.tpa~

ADD_KIT ~RAClanFC~
    ~RAClanFC 1 1 1 1 3 1 3 1~
    ~RAClanFC 1 1 1 1 3 1 3 1 1 1 1 2 1 1 1 1 2 1 1 1 1 1 1 1 1 1 1 1 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAClanFC 15 3 3 3 9 3~
	~RAClanFC 0 0 0 0 0 0~
	~RAClanFC 15 3 3 3 9 3~
    ~RAClanFC 15 3 3 3 9 3~
	~RAClanFC 1 1 0 0 0 0 0 0 0~
	~RAClanFC 0 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAClanFC.2da~
	~~
	~0x04000000 8~
	~FC0~
	~PLAT16 HAMM09 BDHELM09 * RING29 RING31 CLCK05 BOOT04 AMUL27 BRAC10 BELT03 * * * * * * AX1H07 WAFLAIL *~
	SAY @501
	SAY @502
	SAY @503

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAClanFC~
		clascolr = ~205 5 79 41 105~
		clswpbon = ~1 0 3~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAClanFC~
      kit_clab = ~RAClanFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 2
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 2
	u_daggers 			= 1
	u_short_swords		= 1
	u_long_swords		= 1
	u_scimitars 		= 1
	u_katanas 			= 1
	u_bastard_swords 	= 1
	u_2hand_swords 		= 1
	u_halberds 			= 1
	u_spears 	 		= 1
	u_darts 			= 1
	u_slings	 		= 1
	u_bows 				= 1
	u_crossbows 		= 1

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~x~
	s_Benediction 	= ~major~
	s_Destruction 	= ~x~
	s_Protection 	= ~major~
	s_War 			= ~major~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~minor~
	s_Dread 		= ~x~
	s_Vigor 		= ~major~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~major~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY "%index%" 5 9 "clab"
    PATCH_IF ("%clab%" STRING_COMPARE_CASE "RAClanFC" = 0) BEGIN
      READ_2DA_ENTRY "%index%" 0 9 "RAClanFC"
      SET "rows" = 0
    END
  END
  BUT_ONLY_IF_IT_CHANGES

OUTER_SET kit = "%RAClanFC%"
ACTION_FOR_EACH creature IN yeslic yeslic5 yeslid BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%creature%.cre~ THEN BEGIN

    COPY_EXISTING ~%creature%.cre~ ~override~
      WRITE_SHORT   0x244 0
      WRITE_BYTE    0x246 "%kit%"
      WRITE_BYTE    0x247 0x40
      REMOVE_MEMORIZED_SPELL ~SPIN112~
      REMOVE_KNOWN_SPELL ~SPIN112~
      BUT_ONLY
  END
  END
  
//EoF
