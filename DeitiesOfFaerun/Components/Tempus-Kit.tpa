//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Battleguard of Tempus
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATemp~
    ~RATemp 2 2 1 2 2 2 2 1~
    ~RATemp 2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATemp 14 3 3 3 12 3~
	~RATemp 0 0 0 0 0 0~
	~RATemp 14 3 3 3 12 3~
    ~RATemp 14 3 3 3 12 3~
	~RATemp 0 0 0 0 0 0 1 1 1~
	~RATemp 1 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RATemp.2da~
	~K_C_H   K_C_HE   K_C_D   K_C_HO~
	~0x04000000 3~
	~Cl0~
	~PLAT18 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SW1H30 HAMM09 *~
	SAY @201
	SAY @202
	SAY @203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATemp~
		clascolr = ~205 83 65 41 93~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 0 1 1 0 0 1~
		hpclass = ~HPWAR~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 2
	u_hammers	 		= 2
	u_maces 	 		= 2
	u_flails 			= 2
	u_axes 				= 2
	u_daggers 			= 2
	u_short_swords		= 2
	u_long_swords		= 2
	u_scimitars 		= 2
	u_katanas 			= 2
	u_bastard_swords 	= 2
	u_2hand_swords 		= 2
	u_halberds 			= 2
	u_spears 	 		= 2
	u_darts 			= 0
	u_slings	 		= 0
	u_bows 				= 0
	u_crossbows 		= 0

	STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~major~
	s_Death 		= ~x~
	s_Benediction 	= ~major~
	s_Destruction 	= ~major~
	s_Protection 	= ~minor~
	s_War 			= ~major~
	s_Exploration   = ~minor~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~minor~
	s_Vigor 		= ~major~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAHSTEMP.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSTEMP.itm" override SAY NAME1 @208 SAY NAME2 @208 SAY DESC @209
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATemp~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  
END

ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN

COPY ~DeitiesOfFaerun\Spl\Import\Tempus\Complement~ override

ADD_PROJECTILE ~DeitiesOfFaerun\Spl\Import\Tempus\Complement\PRAYB.PRO~

ADD_PROJECTILE ~DeitiesOfFaerun\Spl\Import\Tempus\Complement\PRAYG.PRO~

COPY "DeitiesOfFaerun\Spl\Import\Tempus\RAPR316.spl" ~override\RATEMP1.spl~ SAY NAME1 @204 SAY UNIDENTIFIED_DESC @205    //Prayer
LPM ~SPELL_TO_INNATE~

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@204) STR_VAR bam_file = RAPRAYIC RET iconpray = icon END

COPY "DeitiesOfFaerun\Spl\Import\Tempus\RAPRAYG.spl" override                                                           //Prayer Good
LPF ALTER_SPELL_HEADER INT_VAR projectile = %PRAYG% END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 183 parameter2 = iconpray END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 40256 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@206) END

COPY "DeitiesOfFaerun\Spl\Import\Tempus\RAPRAYB.spl" override                                                           //Prayer Bad
LPF ALTER_SPELL_HEADER INT_VAR projectile = %PRAYB% END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 40257 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@207) END


END

ACTION_IF GAME_IS ~iwdee~ BEGIN 

COPY_EXISTING ~SPPR316.spl~ ~override/RATEMP1.spl~
LPM ~SPELL_TO_INNATE~

END

COPY_EXISTING ~SPCL321.spl~ ~override/RATEMP2.spl~                                                           //Enrage

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		103 104 105 106 107 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATemp~)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@202)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~TEMPS~ ~TEMPL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//Hide the original kit

COPY_EXISTING
~K_C_H.2DA~  override
~K_C_HO.2DA~ override 
~K_C_HE.2DA~ override
~K_C_HL.2DA~ override
~K_C_E.2DA~  override
~K_C_D.2DA~  override
~K_C_G.2DA~  override
	READ_2DA_ENTRIES_NOW ~READ_K_C_H~ 2
	FOR (i = READ_K_C_H - 1; i >= 0; --i) BEGIN
		READ_2DA_ENTRY_FORMER ~READ_K_C_H~ i 1 kit
		PATCH_MATCH kit WITH
			41 BEGIN
				REMOVE_2DA_ROW i 2
			END
			DEFAULT
		END
	END
BUT_ONLY

//Hide the kit from Dual Classing

  COPY_EXISTING ~DUALCLAS.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN 
      READ_2DA_ENTRY_FORMER rows row 0 ~kit_name~
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~OHTEMPUS~) BEGIN
        SET tyr_row = %row%
      END
    END
    FOR (col = 1; col < 7; ++col) BEGIN 
      PATCH_IF IS_AN_INT %tyr_row% BEGIN
        SET_2DA_ENTRY %tyr_row% col cols 0
      END
    END
  BUT_ONLY

//EoF