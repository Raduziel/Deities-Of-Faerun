//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Battleguard of Tempus
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATemp~
    ~RATemp 2 2 1 2 2 2 2 1~
    ~RATemp 2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 0 0 0 0 0 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATemp 14 3 3 3 12 3~
	~RATemp 0 0 0 0 0 0~
	~RATemp 14 3 3 3 12 3~
    ~RATemp 14 3 3 3 12 3~
	~RATemp 0 0 0 0 0 0 1 1 1~
	~RATemp 1 0 0 0 0 0~
	~DeitiesOfFaerun/Tables/RATemp.2da~
	~K_C_H   K_C_HE   K_C_D   K_C_HO~
	~0x00000000 3~
	~Cl0~
	~PLAT18 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SW1H30 HAMM09 *~
	SAY @201
	SAY @202
	SAY @203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATemp~
		clascolr = ~93 83 65 94 100~
		clsrcreq = ~1 0 1 1 0 0 1~
		hpclass = ~HPWAR~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RATEMP~  clabfile = ~RATEMP~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSTEMP.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSTEMP~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSTEMP.itm~ SAY NAME1 @204 SAY NAME2 @204 SAY DESC @205
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RATemp + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~TEMPS~
WRITE_ASCII 0x058 ~TEMPL~

//Spells

//Enrage

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]BERSERKER_RAGE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~BERSERKER_RAGE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATEMP1.spl~
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%D~ resource = EVAL ~RATEMP2D~	END
LPF	CLONE_EFFECT INT_VAR silent = 1 match_opcode = 206 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATEMP1~ END

COPY_EXISTING	~%spell_res%D.spl~	~override/RATEMP1D.spl~

COPY_EXISTING	~%spell_res%.spl~	override
LPF	CLONE_EFFECT INT_VAR silent = 1 match_opcode = 206 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RATEMP1~ END

END

//Prayer

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_PRAYER[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_PRAYER~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATEMP2.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/OHVRNP.PRO~

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/OHVRPO.PRO~

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316.spl~ 1 3 CLERIC_PRAYER
SAY NAME1 @999948 SAY NAME2 @999948 SAY UNIDENTIFIED_DESC @999949 SAY DESC @999949
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316B.BAM~	~override/%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316C.BAM~	~override/%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/#PRAYERB.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRNP% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40257 parameter1 = EVAL ~%RA_Penalty%~ END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@999947) STR_VAR bam_file = RAALLIC RET iconpray = icon END
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/#PRAYERG.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRPO% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40256 parameter1 = EVAL ~%RA_Bonus%~ END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 183   parameter2 = iconpray END

COPY_EXISTING	~%spell_res%.spl~	~override/RATEMP2.spl~
LPM ~SPELL_TO_INNATE~

END

//Heroes' Feast

COPY ~DeitiesOfFaerun/Spl/RATEMP3.spl~ override 
SAY NAME1 @206 SAY UNIDENTIFIED_DESC @207

ACTION_IF !FILE_EXISTS_IN_GAME ~RAFeast.itm~ BEGIN

COPY ~DeitiesOfFaerun/Itm/HeroesFeast/RAFeast.itm~ override 
SAY NAME1 @208 SAY NAME2 @208 SAY DESC @209 SAY UNIDENTIFIED_DESC @209

COPY ~DeitiesOfFaerun/Itm/HeroesFeast/RAFeastL.bam~ override

COPY ~DeitiesOfFaerun/Itm/HeroesFeast/RAFeastS.bam~ override

END

//Multiclass CLAB

COPY_EXISTING ~RATemp.2da~ ~override/RATempFC.2da~
SET_2DA_ENTRY 4 25 50 ~AP_RAFTEMP~
READ_2DA_ENTRIES_NOW read 51
	FOR (i = 1; i < 51; ++i)	BEGIN
		FOR (j = 0; j < read; ++j)	BEGIN
			PATCH_IF	EVAL $read(~%j%~ ~%i%~) STRING_EQUAL ~****~ BEGIN
				SET_2DA_ENTRY j i 51 ~AP_RAHPFIX~
				SET	j = read
			END
		END
END
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		103 104 105 106 107 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATemp~)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~TEMPS~ ~TEMPL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~TEMPHERO~ BEGIN
COPY ~DeitiesOfFaerun/Spl/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//Hide the original kit

COPY_EXISTING
~K_C_H.2DA~  override
~K_C_HO.2DA~ override 
~K_C_HE.2DA~ override
~K_C_HL.2DA~ override
~K_C_E.2DA~  override
~K_C_D.2DA~  override
~K_C_G.2DA~  override
	READ_2DA_ENTRIES_NOW ~READ_K_C_H~ 2
	FOR (i = READ_K_C_H - 1; i >= 0; --i) BEGIN
		READ_2DA_ENTRY_FORMER ~READ_K_C_H~ i 1 kit
		PATCH_MATCH kit WITH
			41 BEGIN
				REMOVE_2DA_ROW i 2
			END
			DEFAULT
		END
	END
BUT_ONLY

//Hide the kit from Dual Classing

  COPY_EXISTING ~DUALCLAS.2da~ override
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN 
      READ_2DA_ENTRY_FORMER rows row 0 ~kit_name~
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~OHTEMPUS~) BEGIN
        SET tyr_row = %row%
      END
    END
    FOR (col = 1; col < 7; ++col) BEGIN 
      PATCH_IF IS_AN_INT %tyr_row% BEGIN
        SET_2DA_ENTRY %tyr_row% col cols 0
      END
    END
  BUT_ONLY

//EoF