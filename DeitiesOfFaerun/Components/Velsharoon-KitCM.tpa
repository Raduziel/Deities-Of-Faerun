//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						Necrophant of Velsharoon - Cleric/Mage
//__________________________________________________________________________________
//__________________________________________________________________________________

INCLUDE ~DeitiesOfFaerun\Lib\Multiclass\qd_multiclass.tpa~

ADD_KIT ~RAVelsCM~
    ~RAVelsCM 0 0 0 0 1 1 0 1~
    ~RAVelsCM 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAVelsCM 3 3 3 11 16 3~
	~RAVelsCM 0 0 0 0 0 0~
	~RAVelsCM 3 3 3 11 16 3~
    ~RAVelsCM 3 3 3 11 16 3~
	~RAVelsCM 0 0 1 0 0 1 0 0 1~
	~RAVelsCM 1 1 1 1 1 1~
	~DeitiesOfFaerun\Tables\RAVelsCM.2da~
	~K_CM_H K_CM_E~
	~0x04000000 14~
	~CM0~
	~WA2ROBE SHLD22 HELM07 * RING07 RING39 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF11 BLUN25 *~
	SAY @3201
	SAY @3202
	SAY @3203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAVelsCM~
		clascolr = ~17 93 137 87 17~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Kjeron's monster code for shenanigans.

OUTER_SPRINT	temp	~PLACEHOLDER~	// any non-space text (label for CLAB row)

OUTER_FOR (i = 0; i < 50; ++i)	BEGIN
	OUTER_SPRINT	temp ~%temp% ****~	//	new row for CLABFILE
END
ACTION_DEFINE_ASSOCIATIVE_ARRAY	clabs	BEGIN
	~RAVELSCM.2DA~ => 0
END

COPY_EXISTING_REGEXP ~^SPWI[1-7]\([0-4][1-9]\|\|10\|20\|30\|40\|50\)\.spl$~ override
	SET	$hidden(~%SOURCE_RES%~) = 0	//	setup variable for each spell
BUT_ONLY

COPY_EXISTING ~HIDESPL.2DA~	override
	COUNT_2DA_COLS	2
	READ_2DA_ENTRIES_NOW	~READ_HIDESPL~ 2
	FOR (i = 0; i < READ_HIDESPL; ++i)	BEGIN
		READ_2DA_ENTRY_FORMER ~READ_HIDESPL~ i 0 res
		READ_2DA_ENTRY_FORMER ~READ_HIDESPL~ i 1 value
		PATCH_IF IS_AN_INT value BEGIN
			SET $hidden(~%res%~) = value	// update as necessary
		END
	END
BUT_ONLY

OUTER_SPRINT animate ~~
ACTION_IF	FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_ANIMATE_DEAD[ %TAB%%WNL%%MNL%%LNL%]~)	BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_ANIMATE_DEAD~	RET animate = spell_res	END
END	//	find the spell in SPELL.IDS (because SR changes some of these)

OUTER_SPRINT finger ~~
ACTION_IF	FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_FINGER_OF_DEATH[ %TAB%%WNL%%MNL%%LNL%]~)	BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_FINGER_OF_DEATH~	RET finger = spell_res	END
END	//	find the spell in SPELL.IDS (because SR changes some of these)

OUTER_SPRINT soul ~~
ACTION_IF	FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_SOUL_EATER[ %TAB%%WNL%%MNL%%LNL%]~)	BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_SOUL_EATER~	RET soul = spell_res	END
END	//	find the spell in SPELL.IDS (because SR changes some of these)

OUTER_SPRINT drain ~~
ACTION_IF	FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_LARLOCH_MINOR_DRAIN[ %TAB%%WNL%%MNL%%LNL%]~)	BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_LARLOCH_MINOR_DRAIN~	RET drain = spell_res	END
END	//	find the spell in SPELL.IDS (because SR changes some of these)

COPY_EXISTING_REGEXP ~^SPWI[1-7]\([0-4][1-9]\|\|10\|20\|30\|40\|50\)\.spl$~ override
	PATCH_IF	! ~%SOURCE_RES%~ STRING_EQUAL ~%animate%~ AND ! ~%SOURCE_RES%~ STRING_EQUAL ~%finger%~ AND ! ~%SOURCE_RES%~ STRING_EQUAL ~%soul%~ AND ! ~%SOURCE_RES%~ STRING_EQUAL ~%drain%~	BEGIN	// not either of these spells
		PATCH_IF $hidden(~%SOURCE_RES%~) = 0 BEGIN	//	not normally hidden
			READ_SHORT	0x1c	type
			READ_SHORT	0x25	school
			READ_SHORT	0x34	level
			PATCH_IF (type = 1) AND (school = 7) BEGIN
				PATCH_IF	level > 6	BEGIN
					SET	column = (level * 2)		//	column/level of CLABFILE to add spell
				END	ELSE	BEGIN
					SET	column = (level * 2 - 1)	//	column/level of CLABFILE to add spell
				END
				TEXT_SPRINT	file ~%SOURCE_RES%~
				INNER_PATCH_SAVE	fnpfile ~%file%~	BEGIN	WRITE_ASCII 0 ~D5FP~	END
				PATCH_IF	! FILE_EXISTS_IN_GAME ~%fnpfile%~	BEGIN
					INNER_PATCH_SAVE	newfile ~%file%~ BEGIN	WRITE_ASCII 0 ~VELS~	END
					PATCH_IF	! FILE_EXISTS_IN_GAME ~%newfile%~	BEGIN
						INNER_ACTION BEGIN
							COPY_EXISTING ~%file%.spl~	~override\%newfile%.spl~
								WRITE_LONG	0x1c	2	//	priest
								LPF	CLONE_EFFECT INT_VAR	match_opcode = 206 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
								LPF	CLONE_EFFECT INT_VAR	match_opcode = 321 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
								LPF	ALTER_EFFECT INT_VAR	match_opcode = 318 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
								LPF	ALTER_EFFECT INT_VAR	match_opcode = 324 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
						END
								LPF	CLONE_EFFECT INT_VAR	match_opcode = 206 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
								LPF	CLONE_EFFECT INT_VAR	match_opcode = 321 STR_VAR silent = 1 match_resource = EVAL ~%file%~ resource = EVAL ~%newfile%~	END
					END
					INNER_PATCH_SAVE	newrow ~%temp%~ BEGIN	SET_2DA_ENTRY 0 column 50 ~GA_%newfile%~	END	// replace proper column of '****' with AP_newfile
				END	ELSE	BEGIN
					INNER_PATCH_SAVE	newrow ~%temp%~ BEGIN	SET_2DA_ENTRY 0 column 50 ~GA_%fnpfile%~	END	// use fnp spell instead if it exists, replace proper column of '****' with AP_fnpfile
				END
				INNER_ACTION	BEGIN
					ACTION_PHP_EACH	clabs AS one => two 	BEGIN
						APPEND	~%one%~ ~%newrow%~ // add new row to each CLABFILE
					END
				END
			END
		END
	END
BUT_ONLY
ACTION_PHP_EACH	clabs AS one => two 	BEGIN
	COPY_EXISTING	~%one%~	override 	PRETTY_PRINT_2DA
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAVelsCM~
      kit_clab = ~RAVelsCM~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 0
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

    STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~x~
	s_Death 		= ~major~
	s_Benediction 	= ~x~
	s_Destruction 	= ~major~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~x~
	s_Knowledge 	= ~major~
	s_Deception 	= ~minor~
	s_Thought 		= ~x~
	s_Dread 		= ~major~
	s_Vigor 		= ~x~
	s_Affliction 	= ~minor~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~minor~
	s_Magic 		= ~major~
	s_Perdition     = ~major~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAMVels.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RACMVels.itm" override SAY NAME1 @3207 SAY NAME2 @3207 SAY DESC @3208
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAVelsCM + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END
		  
COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAVelsCM~)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAVelsCM~) parameter2 = 9 END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAVelsCM~) parameter2 = 9 END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "2A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAVelsCM~) parameter2 = 9 END
	END
END
END
BUT_ONLY

//EoF