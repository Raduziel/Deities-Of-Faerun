//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Bloodstalker of Urdlen
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAUrdl~
    ~RAUrdl 1 0 0 0 1 1 0 1~
    ~RAUrdl 1 0 0 0 1 1 0 1 1 1 1 1 1 1 1 1 0 0 1 1 0 0 0 1 1 1 1 0 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAUrdl 11 3 3 3 9 3~
	~RAUrdl 0 0 0 0 0 0~
	~RAUrdl 11 3 3 3 9 3~
    ~RAUrdl 11 3 3 3 9 3~
	~RAUrdl 0 0 0 0 0 0 0 0 1~
	~RAUrdl 0 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RAUrdl.2da~
	~K_C_G~
	~0x00000000 3~
	~Cl0~
	~LEAT19 NEBDAG HELM06 * RING30 RING39 CLCK08 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * SW1H30 SW1H35 *~
	SAY @4701
	SAY @4702
	SAY @4703

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAUrdl~
		clascolr = ~47 127 183 182 66~
		clswpbon = ~0 0 3~
		clsrcreq = ~0 0 0 0 0 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAURDL~  clabfile = ~RAURDL~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSURDL.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSURDL~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSURDL.itm~ SAY NAME1 @4704 SAY NAME2 @4704 SAY DESC @4705
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAUrdl + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~URDLS~
WRITE_ASCII 0x058 ~URDLL~

//Spells

COPY "DeitiesOfFaerun\Spl\RAURDLF.spl" override

//Cause Light Wounds

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CAUSE_LIGHT_WOUNDS[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CAUSE_LIGHT_WOUNDS~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL1.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/CauseLightWounds/SPPR114.spl~ 1 1 CLERIC_CAUSE_LIGHT_WOUNDS
SAY NAME1 @999979 SAY NAME2 @999979 SAY UNIDENTIFIED_DESC @999980 SAY DESC @999980
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/CauseLightWounds/SPPR114B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/CauseLightWounds/SPPR114C.BAM~	~override\%spell_res%C.BAM~

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL1.spl~
LPM ~SPELL_TO_INNATE~

END

//Blindness

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_BLINDNESS[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_BLINDNESS~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL2.spl~
LPM ~SPELL_TO_INNATE~

END

//Cloak of Fear

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CLOAK_OF_FEAR[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CLOAK_OF_FEAR~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL3.spl~
LPM ~SPELL_TO_INNATE~

END

//Dimension Door

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_DIMENSION_DOOR[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_DIMENSION_DOOR~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL4.spl~
LPM ~SPELL_TO_INNATE~

END

//Creeping Doom

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CREEPING_DOOM[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CREEPING_DOOM~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL5.spl~
LPM ~SPELL_TO_INNATE~

END

//Slay Living

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_SLAY_LIVING[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_SLAY_LIVING~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL6.spl~
LPM ~SPELL_TO_INNATE~

END

//Wither

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CAUSE_LIGHT_WOUNDS[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CAUSE_LIGHT_WOUNDS~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL7.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/Wither/SPPR740.spl~ 1 7 CLERIC_WITHER
SAY NAME1 @9999168 SAY NAME2 @9999168 SAY UNIDENTIFIED_DESC @9999169 SAY DESC @9999169
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT	   STR_VAR silent = 1 match_resource = EVAL ~%SOURCE_RES%~	resource = EVAL ~%spell_res%~ END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 40835 parameter1 = RESOLVE_STR_REF (@9999170) END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Wither/SPPR740B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Wither/SPPR740C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Wither/SPPR740.EFF~	~override\%spell_res%.EFF~

COPY_EXISTING	~%spell_res%.spl~	~override/RAURDL7.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

ACTION_IF !GAME_IS ~iwdee~ BEGIN

COPY_EXISTING ~RAUrdl.2da~ ~override/RAUrdlCT.2da~
SET_2DA_ENTRY 9 25 50 ~AP_RATURDL~
PRETTY_PRINT_2DA

END ELSE BEGIN

COPY_EXISTING ~RAUrdl.2da~ ~override/RAUrdlCT.2da~
SET_2DA_ENTRY 9 25 50 ~AP_RATURDL~
SET_2DA_ENTRY 1  7 50 ~AP_#EVASION~
PRETTY_PRINT_2DA

END

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		97 100 101 102 107 115  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAUrdl + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~URDLS~ ~URDLL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF