//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						          Arachne of Lolth
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RALolt~
    ~RALolt 0 0 0 0 1 1 0 1~
    ~RALolt 0 0 0 0 1 1 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RALolt 3 3 3 3 13 3~
	~RALolt 0 0 0 0 0 0~
	~RALolt 3 3 3 3 13 3~
    ~RALolt 3 3 3 3 13 3~
	~RALolt 0 0 0 0 0 0 0 0 1~
	~RALolt 0 0 0 0 0 0~
	~DeitiesOfFaerun/Tables/RALolt.2da~
	~K_C_E~
	~0x00000000 3~
	~Cl0~
	~LEAT19 SHLD21 HELM04 * RING28 RING31 CLCK03 BOOT01 AMUL17 BRAC06 BELT04 * * * * * * SW1H30 BLUN17 *~
	SAY @5101
	SAY @5102
	SAY @5103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RALolt~
		clascolr = ~135 47 136 183 66~
		clsrcreq = ~0 0 0 0 0 0 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RALOLT~  clabfile = ~RALOLT~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSLOLT.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSLOLT~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSLOLT.itm~ SAY NAME1 @5104 SAY NAME2 @5104 SAY DESC @5105
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RALolt + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~LOLTS~
WRITE_ASCII 0x058 ~LOLTL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RALOLTF.spl~ override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@5106) END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL ~%RA_Poison%~      END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL ~%RA_Poisoned%~    END

//Spiderwalk

COPY ~DeitiesOfFaerun/Spl/RALolt1.bam~ override

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@5107) STR_VAR bam_file = RALOLT1 RET iconspid = icon END	
COPY ~DeitiesOfFaerun/Spl/RALOLT1.spl~ override SAY NAME1 @5107  SAY UNIDENTIFIED_DESC @5108
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconspid END
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 2; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 1; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 1) match_duration = 24 duration = (18 + (6 * i)) END
	END

//Dispel Magic

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_DISPEL_MAGIC[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_DISPEL_MAGIC~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RALOLT2.spl~
LPM ~SPELL_TO_INNATE~

END

//Spider Spawn

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_SPIDER_SPAWN[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_SPIDER_SPAWN~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RALOLT3.spl~
LPM ~SPELL_TO_INNATE~

END

//True Sight

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_TRUE_SIGHT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_TRUE_SIGHT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RALOLT4.spl~
LPM ~SPELL_TO_INNATE~

END

//Domination

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_DOMINATION[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_DOMINATION~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RALOLT5.spl~
LPM ~SPELL_TO_INNATE~

END

//BAM

ACTION_FOR_EACH file IN ~LOLTS~ ~LOLTL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~LOLTSPID~ BEGIN
COPY ~DeitiesOfFaerun/Spl/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF