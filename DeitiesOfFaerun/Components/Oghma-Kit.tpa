//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Lorekeeper of Oghma
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAOghm~
    ~RAOghm 0 0 0 0 1 1 0 1~
    ~RAOghm 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAOghm 3 3 3 12 14 3~
	~RAOghm 0 0 0 0 0 0~
	~RAOghm 3 3 3 12 14 3~
    ~RAOghm 3 3 3 12 14 3~
	~RAOghm 0 0 0 0 1 0 0 0 0~
	~RAOghm 0 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RAOghm.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HL   K_C_D~
	~0x00000000 3~
	~Cl0~
	~CHAN13 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF20 HAMM09 *~
	SAY @1201
	SAY @1202
	SAY @1203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAOghm~
		clascolr = ~205 254 105 22 202~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAOGHM~  clabfile = ~RAOGHM~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSOGHM.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSOGHM~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSOGHM.itm~ SAY NAME1 @1204 SAY NAME2 @1204 SAY DESC @1205
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAOghm + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~OGHMS~
WRITE_ASCII 0x058 ~OGHML~

//Spells

//Free Action (Feature)

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_FREE_ACTION[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_FREE_ACTION~	RET	spell_res	END

COPY_EXISTING ~%spell_res%.spl~	~override/RAOGHMF.spl~
WRITE_SHORT 0x1C 4                                                    
WRITE_LONG  0x34 1                                                         
WRITE_LONG  0x22 00
FOR (level = 2; level < 51; level += 1) BEGIN
LPF DELETE_SPELL_HEADER INT_VAR silent = 1 header_type = ~-1~ min_level = level  END
END
LPF ALTER_SPELL_HEADER INT_VAR projectile = 0 target = 5 range = 1 speed = 0 location = 0 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 9   END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 50  END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 139 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 141 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 215 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 321 END
LPF ALTER_EFFECT INT_VAR target = 2 timing = 9 duration = 0 resist_dispel = 0 END

END

COPY "DeitiesOfFaerun\Spl\RAOGHM1.spl" override SAY NAME1 @1206 SAY UNIDENTIFIED_DESC @1207 //Legend Lore

//Friends

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_FRIENDS[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_FRIENDS~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAOGHM2.spl~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAOGHM2~ END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAOGHM2~ END

END

//Multiclass CLAB

COPY_EXISTING ~RAOghm.2da~ ~override/RAOghmCM.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAMOGHM~
SET_2DA_ENTRY 6  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAOghm + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAOghm~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~OGHMS~ ~OGHML~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~OGHMLELO~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF