//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					      Mistcaller of Leira - Cleric/Thief
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RALeirCT~
    ~RALeirCT 0 0 0 0 1 1 0 1~
    ~RALeirCT 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RALeirCT 3 11 3 12 14 3~
	~RALeirCT 0 0 0 0 0 0~
	~RALeirCT 3 11 3 12 14 3~
    ~RALeirCT 3 11 3 12 14 3~
	~RALeirCT 0 0 0 0 0 0 0 1 1~
	~RALeirCT 0 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RALeirCT.2da~
	~K_CT_H  K_CT_HE  K_CT_G~
	~0x00000000 15~
	~CT0~
	~* * * * * * * * * * * * * * * * * * * *~
	SAY @3301
	SAY @3302
	SAY @3303

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RALeirCT~
		clascolr = ~100 91 114 92 100~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 0 1 0 0 1 0~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RALeirCT~
      kit_clab = ~RALeirCT~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.tpa~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RALeirCT%	classID = 15 STR_VAR	kitname = ~RALEIR~  END

END

COPY "DeitiesOfFaerun\Spl\RATLeir.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RACTLeir.itm" override SAY NAME1 @3304 SAY NAME2 @3304 SAY DESC @3305
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RALeirCT + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RALeirCT + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "3A" "2A" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RALeirCT~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//EoF