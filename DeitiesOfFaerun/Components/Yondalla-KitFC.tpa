//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						  Horn Guard of Yondalla - Fighter/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAYondFC~
    ~RAYondFC 0 0 0 0 1 1 0 1~
    ~RAYondFC 0 0 0 0 1 1 0 1 2 2 2 2 0 2 2 2 2 2 0 0 2 2 0 0 0 2 2 2 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAYondFC 12 3 3 3 13 3~
	~RAYondFC 0 0 0 0 0 0~
	~RAYondFC 12 3 3 3 13 3~
    ~RAYondFC 12 3 3 3 13 3~
	~RAYondFC 1 0 0 0 0 0 0 0 0~
	~RAYondFC 0 0 0 0 0 0~
	~override\RAYondFC.2da~
	~K_FC_HL~
	~0x00000000 8~
	~FC0~
	~PLAT18 SHLD22 HELM07 * RING29 RING31 CLCK05 BOOT04 AMUL27 BRAC10 BELT07 * * * * * * WAFLAIL WASLING *~
	SAY @4601
	SAY @4602
	SAY @4613

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAYondFC~
		clascolr = ~26 138 54 38 52~
		clswpbon = ~1 0 2~
		clsrcreq = ~0 0 0 0 1 0 0~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAYondFC~
      kit_clab = ~RAYondFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAYondFC%	classID = 8 STR_VAR	kitname = ~RAYOND~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAFYOND.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAFCYOND~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAFCYOND.itm~ SAY NAME1 @4604 SAY NAME2 @4604 SAY DESC @4605
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAYondFC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~YONDS~
WRITE_ASCII 0x058 ~YONDL~

//Spell

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		107 BEGIN
		  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAYondFC~) parameter2 = 9 timing = 2 STR_VAR resource = ~RAYONDF~ END
		END
		DEFAULT
	END
BUT_ONLY

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_LONG 0x18 flags
	READ_BYTE 0x31 proficiency
	PATCH_IF ((flags BAND 0b00000010) == 0b00000010) && !(proficiency = 105) BEGIN
		LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAYondFC + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	END
BUT_ONLY

//EoF