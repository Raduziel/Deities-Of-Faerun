//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Bloodreaver of Garagos
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAGara~
    ~RAGara 1 1 1 1 1 1 1 1~
    ~RAGara 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAGara 14 3 3 3 12 3~
	~RAGara 0 0 0 0 0 0~
	~RAGara 13 3 11 3 9 3~
    ~RAGara 13 3 11 3 9 3~
	~RAGara 0 0 0 0 0 0 0 1 1~
	~RAGara 1 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAGara.2da~
	~K_C_H   K_C_HE   K_C_D   K_C_HO   K_C_HL   K_C_G   K_C_E~
	~0x04000000 3~
	~Cl0~
	~PLAT18 WAFLAIL HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SW2H09 HAMM09 *~
	SAY @3001
	SAY @3002
	SAY @3003

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAGara~
		clascolr = ~30 65 47 41 66~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
		hpclass = ~HPWAR~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 1
	u_daggers 			= 1
	u_short_swords		= 1
	u_long_swords		= 1
	u_scimitars 		= 1
	u_katanas 			= 1
	u_bastard_swords 	= 1
	u_2hand_swords 		= 1
	u_halberds 			= 1
	u_spears 	 		= 1
	u_darts 			= 1
	u_slings	 		= 1
	u_bows 				= 1
	u_crossbows 		= 1

	STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~x~
	s_Death 		= ~x~
	s_Benediction 	= ~major~
	s_Destruction 	= ~major~
	s_Protection 	= ~x~
	s_War 			= ~major~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~x~
	s_Vigor 		= ~major~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~minor~
	s_Fire 			= ~minor~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAGARAF.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Charmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL "%RA_DireCharmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 3 match_opcode = 267 parameter1 = EVAL "%RA_Dominated%" END

COPY "DeitiesOfFaerun\Spl\RAHSGARA.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSGARA.itm" override SAY NAME1 @3008 SAY NAME2 @3008 SAY DESC @3009
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAGara~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  
END

COPY_EXISTING ~SPCL321.spl~ ~override/RAGARA1.spl~                                                           //Enrage

COPY "DeitiesOfFaerun\Spl\RAGARA2.spl" override SAY NAME1 @3004 SAY UNIDENTIFIED_DESC @3005    				 //Chaotic Combat

COPY "DeitiesOfFaerun\Spl\RAGARA22.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA2A.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA2B.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA3.spl" override SAY NAME1 @3006 SAY UNIDENTIFIED_DESC @3007                  //Blood Armor

COPY_EXISTING ~SPPR603.spl~ ~override/RAGARA4.spl~                                                           //Blade Barrier
LPM SPELL_TO_INNATE

ACTION_FOR_EACH file IN ~GARAS~ ~GARAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~GARABLOO~ ~GARACHAO~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF