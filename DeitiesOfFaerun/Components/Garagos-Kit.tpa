//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Bloodreaver of Garagos
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAGara~
    ~RAGara 1 1 1 1 1 1 1 1~
    ~RAGara 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAGara 14 3 3 3 12 3~
	~RAGara 0 0 0 0 0 0~
	~RAGara 13 3 11 3 9 3~
    ~RAGara 13 3 11 3 9 3~
	~RAGara 0 0 0 0 0 0 0 1 1~
	~RAGara 1 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAGara.2da~
	~K_C_H   K_C_HE   K_C_D   K_C_HO   K_C_HL   K_C_G   K_C_E~
	~0x00000000 3~
	~Cl0~
	~PLAT18 WAFLAIL HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SW2H09 HAMM09 *~
	SAY @3001
	SAY @3002
	SAY @3003

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAGara~
		clascolr = ~30 65 47 39 66~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
		hpclass = ~HPWAR~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAGARA~  clabfile = ~RAGARA~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSGARA.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSGARA~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSGARA.itm~ SAY NAME1 @3008 SAY NAME2 @3008 SAY DESC @3009
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAGara + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~GARAS~
WRITE_ASCII 0x058 ~GARAL~

//Spells

COPY "DeitiesOfFaerun\Spl\RAGARAF.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Charmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL "%RA_DireCharmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 3 match_opcode = 267 parameter1 = EVAL "%RA_Dominated%" END

//Enrage

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]BERSERKER_RAGE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~BERSERKER_RAGE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAGARA1.spl~
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%D~ resource = EVAL ~RAGARA1D~	END
LPF	CLONE_EFFECT INT_VAR silent = 1 match_opcode = 206 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAGARA1~ END

COPY_EXISTING	~%spell_res%D.spl~	~override/RAGARA1D.spl~

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR silent = 1 match_opcode = 206 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAGARA1~ END

END                                                           

COPY "DeitiesOfFaerun\Spl\RAGARA2.spl" override SAY NAME1 @3004 SAY UNIDENTIFIED_DESC @3005    				 //Chaotic Combat

COPY "DeitiesOfFaerun\Spl\RAGARA22.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA2A.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA2B.spl" override

COPY "DeitiesOfFaerun\Spl\RAGARA3.spl" override SAY NAME1 @3006 SAY UNIDENTIFIED_DESC @3007                  //Blood Armor

//Blade Barrier

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_BLADE_BARRIER[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_BLADE_BARRIER~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAGARA4.spl~
LPM ~SPELL_TO_INNATE~

END

//BAM

ACTION_FOR_EACH file IN ~GARAS~ ~GARAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~GARABLOO~ ~GARACHAO~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF