//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Deathstalker of Bhaal
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RABhaa~
    ~RABhaa 0 0 0 0 0 0 0 0~
    ~RABhaa 0 0 0 0 0 0 0 0 0 0 1 0 0 0 0 1 1 1 1 0 1 1 1 1 1 1 1 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RABhaa 3 3 12 3 14 3~
	~RABhaa 0 0 0 0 0 0~
	~RABhaa 3 3 12 3 14 3~
    ~RABhaa 3 3 12 3 14 3~
	~RABhaa 0 0 1 0 0 0 0 0 0~
	~RABhaa 0 0 0 1 0 0~
	~DeitiesOfFaerun\Tables\RABhaa.2da~
	~K_C_H K_C_HE K_C_D K_C_HO K_C_E K_C_HL K_C_G~
	~0x00000000 3~
	~Cl0~
	~LEAT21 DAGG14 HELM07 * RING09 RING31 CLCK20 BOOT01 AMUL27 BRAC16 BELT04 * * * * * * SW1H25 HAMM09 *~
	SAY @2401
	SAY @2402
	SAY @2403

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RABhaa~
		clascolr = ~182 140 140 62 186~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RABHAA~  clabfile = ~RABHAA~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSBHAA.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSBHAA~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSBHAA.itm~ SAY NAME1 @2404 SAY NAME2 @2404 SAY DESC @2405
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RABhaa + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~BHAAS~
WRITE_ASCII 0x058 ~BHAAL~

//Spells

COPY "DeitiesOfFaerun\Spl\RABHAA1.spl" override SAY NAME1 @2406 SAY UNIDENTIFIED_DESC  @2407                //Bone Dagger
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 3; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 2; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 2) match_duration = 6 duration = (6 * i) END
	END

COPY "DeitiesOfFaerun\Spl\RABHAA11.spl" override


COPY "DeitiesOfFaerun\Itm\BoneDagger\RABoneDg.itm" override SAY NAME1 @2406 SAY NAME2 @2406 SAY DESC @2406

COPY "DeitiesOfFaerun\Itm\BoneDagger\RABoneDg.bam" override


//Finger of Death

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_FINGER_OF_DEATH[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_FINGER_OF_DEATH~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABHAA2.spl~
LPM ~SPELL_TO_INNATE~

END

COPY "DeitiesOfFaerun\Spl\RABHAA3.spl" override SAY NAME1 @2408 SAY UNIDENTIFIED_DESC @2409                //Plane Skipping

//Aerial Servant

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_AERIAL_SERVANT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_AERIAL_SERVANT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABHAA4.spl~
LPM ~SPELL_TO_INNATE~

END

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 92 93 94 95 99  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RABhaa + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RABhaa~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~BHAAS~ ~BHAAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~BHAABONE~ ~BHAAPLAN~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF