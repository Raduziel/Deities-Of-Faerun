//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					  Windwalker of Shaundakul - Ranger/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAShauRC~
    ~RAShauRC 1 1 1 1 1 1 1 1~
    ~RAShauRC 1 1 1 1 1 1 1 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAShauRC 13 3 11 3 14 3~
	~RAShauRC 0 0 0 0 0 0~
	~RAShauRC 13 3 11 3 14 3~
    ~RAShauRC 13 3 11 3 14 3~
	~RAShauRC 0 0 0 0 0 0 1 1 0~
	~RAShauRC 0 0 0 0 0 0~
	~override\RAShauRC.2da~
	~K_CR_H K_CR_HE~
	~0x00000000 18~
	~CR0~
	~LEAT19 SW1H30 HELM03 * RING09 RING28 CLCK05 BOOT01 AMUL27 BRAC10 BELT06 * * * * * * * STAF15 SW1H25~
	SAY @3701
	SAY @3702
	SAY @3706

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAShauRC~
		clascolr = ~66 79 254 41 6~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 0 1 0 0 0 0~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAShauRC~
      kit_clab = ~RAShauRC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAShauRC%	classID = 18 STR_VAR	kitname = ~RASHAU~ kitname2 = ~RASHAU~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RARSHAU.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RARCSHAU~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RARCSHAU.itm~ SAY NAME1 @3704 SAY NAME2 @3704 SAY DESC @3705
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAShauRC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~SHAUS~
WRITE_ASCII 0x058 ~SHAUL~

//Restrictions
		  
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "D4" "C6" "C2" "C1"  BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAShauRC~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//EoF