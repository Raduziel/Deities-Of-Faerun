//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Magistrati of Azuth
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAAzut~
    ~RAAzut 0 0 0 0 1 1 0 1~
    ~RAAzut 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAAzut 3 3 3 13 14 3~
	~RAAzut 0 0 0 0 0 0~
	~RAAzut 3 3 3 13 14 3~
    ~RAAzut 3 3 3 13 14 3~
	~RAAzut 0 1 0 0 0 0 0 0 0~
	~RAAzut 0 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RAAzut.2da~
	~K_C_H K_C_HE K_C_E K_C_G~
	~0x04000000 3~
	~Cl0~
	~* SHLD22 HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 BLUN25 *~
	SAY @2701
	SAY @2702
	SAY @2703

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAAzut~
		clascolr = ~5 170 141 137 5~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 0 0 1 0~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 0
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

    STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~major~
	s_Death 		= ~x~
	s_Benediction 	= ~minor~
	s_Destruction 	= ~x~
	s_Protection 	= ~major~
	s_War 			= ~x~
	s_Exploration   = ~major~
	s_Knowledge 	= ~major~
	s_Deception 	= ~minor~
	s_Thought 		= ~major~
	s_Dread 		= ~x~
	s_Vigor 		= ~minor~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~minor~
	s_Shadow 		= ~x~
	s_Magic 		= ~major~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAAZUTF.spl" override

COPY "DeitiesOfFaerun\Spl\RAHSAZUT.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSAZUT.itm" override SAY NAME1 @2704 SAY NAME2 @2704 SAY DESC @2705
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAzut~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY_EXISTING ~SPWI102.spl~ ~override/RAAZUT1.spl~    //Armor
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 2

COPY_EXISTING ~SPWI217.spl~ ~override/RAAZUT2.spl~    //Agannazar's Scorcher
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 3

COPY_EXISTING ~SPWI325.spl~ ~override/RAAZUT3.spl~    //Melf's Minute Meteors
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 4

COPY_EXISTING ~SPWI413.spl~ ~override/RAAZUT4.spl~    //Otiluke's Resilient Sphere
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 5

COPY_EXISTING ~SPWI510.spl~ ~override/RAAZUT5.spl~    //Spell Immunity
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 6

COPY_EXISTING ~SPWI606.spl~ ~override/RAAZUT6.spl~    //Protection From Magic Energy
WRITE_SHORT 0x1C 2                        
WRITE_LONG  0x34 7

COPY_EXISTING ~SPWI112.spl~ ~override/RAAZUT7.spl~    //Magic Missile
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPWI304.spl~ ~override/RAAZUT8.spl~    //Fireball
LPM SPELL_TO_INNATE

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAzut~)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@2702)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAzut~) parameter2 = 9 special = RESOLVE_STR_REF (@2702) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAzut~) parameter2 = 9 special = RESOLVE_STR_REF (@2702) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "2A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAAzut~) parameter2 = 9 special = RESOLVE_STR_REF (@2702) END
	END
END
END
BUT_ONLY

ACTION_FOR_EACH file IN ~AZUTS~ ~AZUTL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END


//EoF