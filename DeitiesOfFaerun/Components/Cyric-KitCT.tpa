//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					     Strifeleader of Cyric - Cleric/Thief
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RACyriCT~
    ~RACyriCT 1 0 0 0 1 1 0 1~
    ~RACyriCT 1 0 0 0 1 1 0 1 0 1 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RACyriCT 13 3 3 3 13 3~
	~RACyriCT 0 0 0 0 0 0~
	~RACyriCT 13 3 3 3 13 3~
    ~RACyriCT 13 3 3 3 13 3~
	~RACyriCT 0 0 0 0 0 1 0 0 1~
	~RACyriCT 0 0 0 0 0 0~
	~override/RACyriCT.2da~
	~K_CT_H   K_CT_HE   K_CT_E   K_CT_G   K_CT_HO   K_CT_HL   K_CT_D~
	~0x00000000 15~
	~CT0~
	~PLAT18 SHLD21 HELM03 * RING22 RING39 CLCK07 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 HAMM09 *~
	SAY @301
	SAY @302
	SAY @313

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RACyriCT~
		clascolr = ~99 140 60 62 61~
		clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RACyriCT~
      kit_clab = ~RACyriCT~
      base_class = ~P~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RACyriCT%	classID = 15 STR_VAR	kitname = ~RACYRI~  END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RATCYRI.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RACTCYRI~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RACTCYRI.itm~ SAY NAME1 @304 SAY NAME2 @304 SAY DESC @305
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RACyriCT + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~CYRIS~
WRITE_ASCII 0x058 ~CYRIL~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RACyriCT + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//EoF