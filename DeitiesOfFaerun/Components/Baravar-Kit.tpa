//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Hoodwinker of Baravar
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RABara~
    ~RABara 0 1 1 0 1 0 0 1~
    ~RABara 0 1 1 0 1 0 0 1 0 0 1 0 0 0 0 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RABara 3 15 3 15 9 3~
	~RABara 0 0 0 0 0 0~
	~RABara 3 15 3 15 9 3~
    ~RABara 3 15 3 15 9 3~
	~RABara 0 0 0 1 0 0 0 0 0~
	~RABara 0 0 1 1 0 0~
	~DeitiesOfFaerun\Tables\RABara.2da~
	~K_C_G~
	~0x00000000 3~
	~Cl0~
	~LEAT08 BDSHLD01 HELM14 * RING05 RING28 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * DAGG13 BOW19 *~
	SAY @601
	SAY @602
	SAY @603

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RABara~
		clascolr = ~205 43 66 182 191~
		clswpbon = ~0 0 3~
		clsrcreq = ~0 0 0 0 0 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RABARA~  clabfile = ~RABARA~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSBARA.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSBARA~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSBARA.itm~ SAY NAME1 @604 SAY NAME2 @604 SAY DESC @605
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RABara + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~BARAS~
WRITE_ASCII 0x058 ~BARAL~

//Spells

COPY "DeitiesOfFaerun\Spl\RABARAF.spl" override

//Mirror Image

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_MIRROR_IMAGE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_MIRROR_IMAGE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABARA1.spl~
LPM ~SPELL_TO_INNATE~

END

//Invisibility

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_INVISIBILITY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_INVISIBILITY~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABARA2.spl~
LPM ~SPELL_TO_INNATE~

END

//Dimension Door

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_DIMENSION_DOOR[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_DIMENSION_DOOR~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABARA3.spl~
LPM ~SPELL_TO_INNATE~

END

//Mislead

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_MISLEAD[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_MISLEAD~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABARA4.spl~
LPM ~SPELL_TO_INNATE~

END

//Prismatic Spray

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_PRISMATIC_SPRAY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_PRISMATIC_SPRAY~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABARA5.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

ACTION_IF !GAME_IS ~iwdee~ BEGIN

COPY_EXISTING ~RABara.2da~ ~override/RABaraCT.2da~
SET_2DA_ENTRY 7 25 50 ~AP_RATBARA~
PRETTY_PRINT_2DA

END ELSE BEGIN

COPY_EXISTING ~RABara.2da~ ~override/RABaraCT.2da~
SET_2DA_ENTRY 7 25 50 ~AP_RATBARA~
SET_2DA_ENTRY 1  7 50 ~AP_#EVASION~
PRETTY_PRINT_2DA

END

COPY_EXISTING ~RABara.2da~ ~override/RABaraCM.2da~
SET_2DA_ENTRY 7 25 50 ~AP_RAMBARA~
SET_2DA_ENTRY 7  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 92 93 94 95 97 98 99 100 101 104  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RABara + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "3A" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RABara~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~BARAS~ ~BARAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF