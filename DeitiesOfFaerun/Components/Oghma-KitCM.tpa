//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					   Lorekeeper of Oghma - Cleric/Mage
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAOghmCM~
    ~RAOghmCM 0 0 0 0 1 1 0 1~
    ~RAOghmCM 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAOghmCM 3 3 3 12 14 3~
	~RAOghmCM 0 0 0 0 0 0~
	~RAOghmCM 3 3 3 12 14 3~
    ~RAOghmCM 3 3 3 12 14 3~
	~RAOghmCM 0 0 0 0 1 0 0 0 0~
	~RAOghmCM 0 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAOghmCM.2da~
	~K_CM_H   K_CM_HE   K_CM_E   K_CM_G   K_CM_HL   K_CM_D~
	~0x00000000 14~
	~CM0~
	~CHAN13 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF20 HAMM09 *~
	SAY @1201
	SAY @1202
	SAY @1203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAOghmCM~
		clascolr = ~205 254 105 22 202~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 0~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAOghmCM~
      kit_clab = ~RAOghmCM~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAOghmCM%	classID = 14 STR_VAR	kitname = ~RAOGHM~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAMOGHM.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RACMOGHM~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RACMOGHM.itm~ SAY NAME1 @1206 SAY NAME2 @1206 SAY DESC @1207
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAOghmCM + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~OGHMS~
WRITE_ASCII 0x058 ~OGHML~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAOghmCM + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAOghmCM~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//EoF