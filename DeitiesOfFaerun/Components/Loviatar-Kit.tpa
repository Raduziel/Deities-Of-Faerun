//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						          Pain of Loviatar
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RALovi~
    ~RALovi 0 0 0 0 1 1 0 1~
    ~RALovi 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RALovi 3 3 15 3 15 3~
	~RALovi 0 0 0 0 0 0~
	~RALovi 3 3 15 3 15 3~
    ~RALovi 3 3 15 3 15 3~
	~RALovi 0 0 1 0 0 0 0 0 0~
	~RALovi 1 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RALovi.2da~
	~K_C_H   K_C_HE   K_C_E   K_C_G   K_C_HL   K_C_D   K_C_HO~
	~0x00000000 3~
	~Cl0~
	~LEAT21 * HELM07 * RING46 RING39 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN22 HAMM09 *~
	SAY @2201
	SAY @2202
	SAY @2203

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RALovi~
		clascolr = ~202 105 72 170 200~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RALOVI~  clabfile = ~RALOVI~	END

END

COPY "DeitiesOfFaerun\Spl\RAHSLOVI.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSLOVI.itm" override SAY NAME1 @2217 SAY NAME2 @2217 SAY DESC @2218
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RALovi + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY "DeitiesOfFaerun\Lib\Spheres\Import\Common\RAPAINIC.BAM" override		  

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2214) STR_VAR bam_file = RAPAINIC RET iconpain = icon END		  
COPY "DeitiesOfFaerun\Spl\RALovi1.spl" override SAY NAME1 @2204 SAY UNIDENTIFIED_DESC @2205
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconpain END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1 parameter1 = RESOLVE_STR_REF (@2214) END

COPY "DeitiesOfFaerun\Spl\RALovi2.spl" override SAY NAME1 @2206 SAY UNIDENTIFIED_DESC @2207
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1 parameter1 = RESOLVE_STR_REF (@2215) END

COPY "DeitiesOfFaerun\Spl\RALovi2.bam" override		  

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2215) STR_VAR bam_file = RALOVI2 RET iconburn = icon END		  
COPY "DeitiesOfFaerun\Spl\RALovi21.spl" override
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconburn END

COPY "DeitiesOfFaerun\Spl\RALovi3.spl" override SAY NAME1 @2208 SAY UNIDENTIFIED_DESC @2209
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1 parameter1 = RESOLVE_STR_REF (@2214) END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconpain END

COPY "DeitiesOfFaerun\Spl\RALovi4.spl" override SAY NAME1 @2210 SAY UNIDENTIFIED_DESC @2211
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1 parameter1 = RESOLVE_STR_REF (@2214) END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconpain END

COPY "DeitiesOfFaerun\Spl\RALovi41.spl" override
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconpain END

COPY "DeitiesOfFaerun\Spl\RALovi42.spl" override
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconpain END

//Symbol, Pain (RALOVI5)

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_SYMBOL_OF_PAIN[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_SYMBOL_OF_PAIN~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RALovi5.spl~
SAY NAME1 @999991 SAY UNIDENTIFIED_DESC @999992
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/SymbolPain/ICGHIT.PRO~

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/SymbolPain/ICEGLYP.PRO~

COPY_EXISTING	~ICEGLYP.PRO~	~override~
WRITE_SHORT 0x0214 ICGHIT

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2214) STR_VAR bam_file = RAPAINIC RET iconpain = icon END
COPY ~DeitiesofFaerun/Lib/Spheres/Import/SymbolPain/SPPR714.spl~ ~override/RALovi5.spl~
SAY NAME1 @999991 SAY NAME2 @999991 SAY UNIDENTIFIED_DESC @999992 SAY DESC @999992
LPF ALTER_SPELL_HEADER INT_VAR projectile = %ICEGLYP% END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 18628 parameter1 = RESOLVE_STR_REF (@2214) END
LPF ALTER_EFFECT  	   INT_VAR match_opcode = 142 match_parameter2 = 169 parameter2 = iconpain END
LPM ~SPELL_TO_INNATE~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/SymbolPain/SPPR714B.BAM~	~override~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/SymbolPain/SPPR714C.BAM~	~override~

END

COPY "DeitiesOfFaerun\Spl\RALovi6.spl" override SAY NAME1 @2212 SAY UNIDENTIFIED_DESC @2213

COPY "DeitiesOfFaerun\Spl\RALovi6.eff" override

COPY "DeitiesOfFaerun\Cre\LoviDevil\LoviDevi.cre" override SAY NAME1 @2216 SAY NAME2 @2216

COPY "DeitiesOfFaerun\Cre\LoviDevil\BDERINYE.itm" override

COPY "DeitiesOfFaerun\Cre\LoviDevil\IMMUNE2.itm" override

COPY "DeitiesOfFaerun\Cre\LoviDevil\DemSuc01.itm" override
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1 parameter1 = EVAL "%RA_Drain1%" END

COPY "DeitiesOfFaerun\Cre\LoviDevil\RABDTRAI.itm" override
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 1 parameter1 = EVAL "%RA_Stun%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 2 parameter1 = EVAL "%RA_Stunned%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 3 parameter1 = EVAL "%RA_Poison%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 4 parameter1 = EVAL "%RA_Poisoned%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 5 parameter1 = EVAL "%RA_Charmed%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 6 parameter1 = EVAL "%RA_DireCharmed%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 7 parameter1 = EVAL "%RA_Dominated%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 8 parameter1 = EVAL "%RA_Panic%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 9 parameter1 = EVAL "%RA_MoralFail%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 10 parameter1 = EVAL "%RA_RigidThink%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 11 parameter1 = EVAL "%RA_Confused%" END


COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RALovi + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@2202)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~LOVIS~ ~LOVIL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~LOVIENSN~ ~LOVIFIRE~ ~LOVIPAIN~ ~LOVITOUC~ ~LOVIWHIP~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF