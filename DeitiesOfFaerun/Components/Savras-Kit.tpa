//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						            Seer of Savras
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RASavr~
    ~RASavr 0 0 0 0 1 1 0 1~
    ~RASavr 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RASavr 3 3 3 11 14 3~
	~RASavr 0 0 0 0 0 0~
	~RASavr 3 3 3 11 14 3~
    ~RASavr 3 3 3 11 14 3~
	~RASavr 0 1 0 0 0 0 0 0 0~
	~RASavr 0 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RASavr.2da~
	~K_C_H K_C_HE K_C_E~
	~0x04000000 3~
	~Cl0~
	~* SHLD22 HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 BLUN25 *~
	SAY @3101
	SAY @3102
	SAY @3103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RASavr~
		clascolr = ~24 51 90 95 24~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 0 0 1 0~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 0
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

    STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~x~
	s_Benediction 	= ~minor~
	s_Destruction 	= ~x~
	s_Protection 	= ~major~
	s_War 			= ~x~
	s_Exploration   = ~major~
	s_Knowledge 	= ~major~
	s_Deception 	= ~x~
	s_Thought 		= ~major~
	s_Dread 		= ~x~
	s_Vigor 		= ~minor~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~major~
	s_Shadow 		= ~x~
	s_Magic 		= ~major~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RASavrF.spl" override

COPY "DeitiesOfFaerun\Spl\RAHSSAVR.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSSAVR.itm" override SAY NAME1 @3104 SAY NAME2 @3104 SAY DESC @3105
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY "DeitiesOfFaerun\Spl\RAForeIc.bam" override
LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2908) STR_VAR bam_file = RAFOREIC RET iconfors = icon END
		  
COPY_EXISTING ~SPWI301.spl~ ~override/RASavr1.spl~    											//Clairvoyance
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPWI425.spl~ ~override/RASavr2.spl~    											//Wizard Eye
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR505.spl~ ~override/RASavr3.spl~    											//True Seeing
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR415.spl~ ~override/RASavr4.spl~    											//Farsight
LPM SPELL_TO_INNATE

COPY "DeitiesOfFaerun\Spl\RASavr5.spl" override SAY NAME1 @2906 SAY UNIDENTIFIED_DESC @2907     //Consequence
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconfors END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@3102)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~) parameter2 = 9 special = RESOLVE_STR_REF (@3102) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~) parameter2 = 9 special = RESOLVE_STR_REF (@3102) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "2A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~) parameter2 = 9 special = RESOLVE_STR_REF (@3102) END
	END
END
END
BUT_ONLY

ACTION_FOR_EACH file IN ~SAVRS~ ~SAVRL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SAVRCONS~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END


//EoF