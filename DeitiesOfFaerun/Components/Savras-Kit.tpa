//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						          Seer of Savras
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RASavr~
    ~RASavr 0 0 0 0 1 1 0 1~
    ~RASavr 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RASavr 3 3 3 11 14 3~
	~RASavr 0 0 0 0 0 0~
	~RASavr 3 3 3 11 14 3~
    ~RASavr 3 3 3 11 14 3~
	~RASavr 0 1 0 0 0 0 0 0 0~
	~RASavr 0 0 1 0 0 0~
	~DeitiesOfFaerun/Tables/RASavr.2da~
	~K_C_H K_C_HE K_C_E~
	~0x00000000 3~
	~Cl0~
	~* SHLD22 HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 BLUN25 *~
	SAY @3101
	SAY @3102
	SAY @3103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RASavr~
		clascolr = ~24 146 50 51 81~
		clsrcreq = ~1 1 1 0 0 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RASAVR~  clabfile = ~RASAVR~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSSAVR.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSSAVR~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSSAVR.itm~ SAY NAME1 @3104 SAY NAME2 @3104 SAY DESC @3105
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RASavr + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~SAVRS~
WRITE_ASCII 0x058 ~SAVRL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RASAVRF.spl~ override

ACTION_IF !FILE_EXISTS_IN_GAME ~RAForeIc.bam~ BEGIN
	
	COPY ~DeitiesOfFaerun/Spl/RAForeIc.bam~ override
	LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2910) STR_VAR bam_file = RAFOREIC RET iconfore = icon END

END ELSE BEGIN
	OUTER_SPRINT	file ~RAForeIc~
	COPY_EXISTING ~statdesc.2da~	override
		REPLACE_EVALUATE CASE_INSENSITIVE ~[%WNL%^]\(0x[0-9A-F]+\|[0-9]+\)[ %TAB%]+[0-9\-]+[ %TAB%]+%file%[ %TAB%%WNL%$]~ BEGIN	SET	iconfore = (~%MATCH1%~)	END	~%MATCH0%~
	BUT_ONLY
END
 
//Clairvoyance

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_CLAIRVOYANCE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_CLAIRVOYANCE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASAVR1.spl~
LPM ~SPELL_TO_INNATE~

END   

//Wizard Eye

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_EYE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_EYE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASAVR2.spl~
LPF	CLONE_EFFECT INT_VAR match_opcode = 206 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RASAVR2~ END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	override
LPF	CLONE_EFFECT INT_VAR match_opcode = 206 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RASAVR2~ END

END 

//True Seeing

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_TRUE_SIGHT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_TRUE_SIGHT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASAVR3.spl~
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%D~ resource = EVAL ~RASAVR3D~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%D.spl~	~override/RASAVR3D.spl~

END

//Farsight

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_FAR_SIGHT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_FAR_SIGHT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASAVR4.spl~
LPM ~SPELL_TO_INNATE~

END

COPY ~DeitiesOfFaerun/Spl/RASAVR5.spl~ override SAY NAME1 @2908 SAY UNIDENTIFIED_DESC @2909     //Consequence
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconfore END

//Multiclass CLAB

COPY_EXISTING ~RASavr.2da~ ~override/RASavrCM.2da~
SET_2DA_ENTRY 7 25 50 ~AP_RAMSAVR~
SET_2DA_ENTRY 7  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RASavr + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	~%animation%~ WITH
				~4A~ ~3A~ ~2A~ BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RASavr~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~SAVRS~ ~SAVRL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SAVRCONS~ BEGIN
COPY ~DeitiesOfFaerun/Spl/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF