//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Black Finger of Beshaba
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RABesh~
    ~RABesh 0 0 0 0 1 1 0 1~
    ~RABesh 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RABesh 3 3 3 3 10 3~
	~RABesh 0 0 0 0 0 0~
	~RABesh 3 3 3 3 10 3~
    ~RABesh 3 3 3 3 10 3~
	~RABesh 0 0 0 0 0 0 0 0 1~
	~RABesh 1 0 1 1 0 0~
	~DeitiesOfFaerun\Tables\RABesh.2da~
	~K_C_H   K_C_HE   K_C_HL   K_C_G   K_C_HO~
	~0x00000000 3~
	~Cl0~
	~PLAT12 SHLD22 HELM04 * RING31 RING09 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * BLUN14 HAMM09 *~
	SAY @3501
	SAY @3502
	SAY @3503

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RABesh~
		clascolr = ~44 139 117 140 119~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 0 1 0 1 1 1~
END

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RABESH~  clabfile = ~RABESH~	END

END

COPY "DeitiesOfFaerun\Spl\RAHSBesh.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSBesh.itm" override SAY NAME1 @3512 SAY NAME2 @3512 SAY DESC @3513
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RABesh + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END


COPY "DeitiesOfFaerun\Spl\RABesh1.spl"  override SAY NAME1 @3504 SAY UNIDENTIFIED_DESC @3505	             //Bad Luck

//Doom

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_DOOM[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_DOOM~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABesh2.spl~
LPM ~SPELL_TO_INNATE~

END

COPY "DeitiesOfFaerun\Spl\RABesh3.spl"  override SAY NAME1 @3506 SAY UNIDENTIFIED_DESC @3507	             //Bane of Beshaba

//Lower Resistance

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_LOWER_RESISTANCE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_LOWER_RESISTANCE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABesh4.spl~
LPM ~SPELL_TO_INNATE~

END

COPY "DeitiesOfFaerun\Spl\RABesh5.spl"  override SAY NAME1 @3508 SAY UNIDENTIFIED_DESC @3509	             //Misfortune


COPY "DeitiesOfFaerun\Spl\RABesh6.spl"  override SAY NAME1 @3510 SAY UNIDENTIFIED_DESC @3511	             //Beshaba's Mercy

//Black Blade of Disaster

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_BLACK_BLADE_OF_DISASTER[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_BLACK_BLADE_OF_DISASTER~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABesh7.spl~
LPM ~SPELL_TO_INNATE~

END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RABesh + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@3502)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~BESHS~ ~BESHL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~BESHBLUC~ ~BESHBANE~ ~BESHMISF~ ~BESHMERC~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF