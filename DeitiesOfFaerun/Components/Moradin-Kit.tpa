//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						       Sonnlinor of Moradin
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAMora~
    ~RAMora 1 1 1 1 1 1 1 1~
    ~RAMora 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAMora 10 3 3 3 11 3~
	~RAMora 0 0 0 0 0 0~
	~RAMora 10 3 3 3 11 3~
    ~RAMora 10 3 3 3 11 3~
	~RAMora 1 1 0 0 0 0 0 0 0~
	~RAMora 1 0 0 0 0 0~
	~DeitiesOfFaerun/Tables/RAMora.2da~
	~K_C_D~
	~0x00000000 3~
	~Cl0~
	~PLAT09 HAMM09 BDHELM09 * RING09 RING29 CLCK05 BOOT01 AMUL27 BRAC10 BELT03 * * * * * * AX1H07 WAFLAIL *~
	SAY @5401
	SAY @5402
	SAY @5403

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAMora~
		clascolr = ~235 135 64 8 72~
		clsrcreq = ~0 0 0 1 0 0 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAMORA~  clabfile = ~RAMORA~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSMORA.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSMORA~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSMORA.itm~ SAY NAME1 @5404 SAY NAME2 @5404 SAY DESC @5405
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAMora + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~MORAS~
WRITE_ASCII 0x058 ~MORAL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RAMORAF.spl~ override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@5406) END

//Command

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_COMMAND[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_COMMAND~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA1.spl~
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 324 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMORA1~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	override
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 324 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMORA1~	END

END		  

//Spiritual Hammer

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_SPIRITUAL_HAMMER[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_SPIRITUAL_HAMMER~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA2.spl~
LPM ~SPELL_TO_INNATE~

END

//Dispel Magic

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_DISPEL_MAGIC[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_DISPEL_MAGIC~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA3.spl~
LPM ~SPELL_TO_INNATE~

END

//Flame Strike

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_FLAME_STRIKE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_FLAME_STRIKE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA4.spl~
LPM ~SPELL_TO_INNATE~

END

//True Seeing

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_TRUE_SIGHT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_TRUE_SIGHT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA5.spl~
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%D~ resource = EVAL ~RAMORA5D~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%D.spl~	~override/RAMORA5D.spl~

END

//Defensive Harmony

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_DEFENSIVE_HARMONY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_DEFENSIVE_HARMONY~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA6.spl~
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMORA6~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	override
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMORA6~	END

END

//Aura of Flaming Death

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_AURA_OF_FLAMING_DEATH[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_AURA_OF_FLAMING_DEATH~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMORA7.spl~
LPF	ADD_SPELL_EFFECT	INT_VAR	opcode = 321 target = 2 parameter2 = 2 STR_VAR insert_point = 0 resource = EVAL ~%spell_res%~ END
LPF	ADD_SPELL_EFFECT	INT_VAR	opcode = 321 target = 2 parameter2 = 2 STR_VAR insert_point = 0 resource = EVAL ~RAMORA7~     END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%.spl~	override
LPF	ADD_SPELL_EFFECT	INT_VAR	opcode = 321 target = 2 parameter2 = 2 STR_VAR insert_point = 0 resource = EVAL ~RAMORA7~     END

END

//Multiclass CLAB

COPY_EXISTING ~RAMora.2da~ ~override/RAMoraFC.2da~
SET_2DA_ENTRY 12 25 50 ~AP_RAFMORA~
PRETTY_PRINT_2DA

//BAM

ACTION_FOR_EACH file IN ~MORAS~ ~MORAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF