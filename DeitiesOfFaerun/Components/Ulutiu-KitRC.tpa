//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						  Iceguardian of Ulutiu - Ranger/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAUlutRC~
    ~RAUlutRC 1 1 1 1 1 1 1 1~
    ~RAUlutRC 1 1 1 1 1 1 1 1 0 0 2 0 0 0 0 2 0 0 2 0 0 0 0 0 0 2 2 0 2 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAUlutRC 12 3 3 3 14 3~
	~RAUlutRC 0 0 0 0 0 0~
	~RAUlutRC 12 3 3 3 14 3~
    ~RAUlutRC 12 3 3 3 14 3~
	~RAUlutRC 0 1 0 0 0 0 0 0 0~
	~RAUlutRC 0 0 0 0 0 0~
	~override/RAUlutRC.2da~
	~K_CR_H~
	~0x00000000 18~
	~CR0~
	~LEAT20 * HELM04 * RING09 RING31 CLCK04 BOOT01 AMUL27 BRAC10 BELT10 * * * * * * SPER08 BOW19 *~
	SAY @4101
	SAY @4102
	SAY @4112

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAUlutRC~
		clascolr = ~23 42 208 38 1~
		clsrcreq = ~1 0 0 0 0 0 0~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAUlutRC~
      kit_clab = ~RAUlutRC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun/Lib/Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAUlutRC%	classID = 18 STR_VAR	kitname = ~RAULUT~ kitname2 = ~RAULUT~  END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RARULUT.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RARCULUT~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RARCULUT.itm~ SAY NAME1 @4104 SAY NAME2 @4104 SAY DESC @4105
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAUlutRC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~ULUTS~
WRITE_ASCII 0x058 ~ULUTL~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 92 93 94 95 97 99 100 101 102 103 104 107 115  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAUlutRC + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	~%animation%~ WITH
				~4A~ ~3A~ ~D1~ ~D2~ ~D3~ ~D4~ ~C0~ ~C1~ ~C2~ ~C3~ ~C4~ ~C5~ ~C6~ BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUlutRC~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//The following code was built by Kjeron

COPY_EXISTING_REGEXP ~^.+\.eff$~ override
	TO_UPPER	SOURCE_RES
	READ_LONG 0x10 opcode
	READ_LONG 0x20 parameter2
	PATCH_MATCH opcode WITH
		12 WHEN parameter2 = 0x80000 BEGIN
			SET $ra_ulutiu_fire_eff(~%SOURCE_RES%~) = 1
		END  // Fire Damage
		DEFAULT
	END
BUT_ONLY // Scan through EFFs for offending effect
COPY_EXISTING_REGEXP ~^.+\.spl$~ override
	TO_UPPER	SOURCE_RES
	SET	ab_size = 0x28
	READ_LONG 0x64 ab_off
	READ_SHORT 0x68 ab_num
	READ_LONG 0x6a fx_off
	FOR (i = 0; i < ab_num; ++i) BEGIN
		READ_SHORT (ab_off + i * ab_size + 0x1e) fx_num
		READ_SHORT (ab_off + i * ab_size + 0x20) fx_idx
		FOR (j = 0; j < fx_num; ++j) BEGIN
			READ_SHORT	(fx_off + (fx_idx + j) * 0x30)	opcode
			READ_LONG	(fx_off + (fx_idx + j) * 0x30 + 0x8)	parameter2
			READ_ASCII	(fx_off + (fx_idx + j) * 0x30 + 0x14)	resource TO_UPPER resource
			PATCH_IF (opcode = 12 AND parameter2 = 0x80000) BEGIN
				SET $ra_ulutiu_fire_spl(~%SOURCE_RES%~) = 1
				SET i = ab_num
				SET J = fx_num
			END // Fire damage
			PATCH_MATCH opcode WITH
				12 WHEN parameter2 = 0x80000 BEGIN
					SET $ra_ulutiu_fire_spl(~%SOURCE_RES%~) = 1
					SET i = ab_num
					SET j = fx_num
				END  // Fire Damage
				177 248 249 272 283 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_eff(~%resource%~) BEGIN
					SET $ra_ulutiu_fire_spl(~%SOURCE_RES%~) = 1
					SET i = ab_num
					SET j = fx_num
				END // EFF files that deal fire damage
				DEFAULT
			END
		END
	END
BUT_ONLY // Scan through spells for offending effect
COPY_EXISTING_REGEXP ~^.+\.eff$~ override
	TO_UPPER	SOURCE_RES
	READ_LONG 0x10 opcode
	READ_LONG 0x20 parameter2
	PATCH_MATCH opcode WITH
		146 148 232 326 333 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_spl(~%resource%~) BEGIN
			SET $ra_ulutiu_fire_eff(~%SOURCE_RES%~) = 1
		END  // Spells that deal fire damage
		DEFAULT
	END
BUT_ONLY // Scan through EFFs for casting offending spells
COPY_EXISTING_REGEXP ~^.+\.spl$~ override
	TO_UPPER	SOURCE_RES
	SET	ab_size = 0x28
	READ_LONG 0x64 ab_off
	READ_SHORT 0x68 ab_num
	READ_LONG 0x6a fx_off
	FOR (i = 0; i < ab_num; ++i) BEGIN
		READ_SHORT (ab_off + i * ab_size + 0x1e) fx_num
		READ_SHORT (ab_off + i * ab_size + 0x20) fx_idx
		FOR (j = 0; j < fx_num; ++j) BEGIN
			READ_SHORT	(fx_off + (fx_idx + j) * 0x30)	opcode
			READ_ASCII	(fx_off + (fx_idx + j) * 0x30 + 0x14)	resource TO_UPPER resource
			PATCH_MATCH opcode WITH
				177 248 249 272 283 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_eff(~%resource%~) BEGIN
					SET $ra_ulutiu_fire_spl(~%SOURCE_RES%~) = 1
					SET i = ab_num
					SET j = fx_num
				END // EFF files that cast fire damage spells
				146 148 232 326 333 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_spl(~%resource%~) BEGIN
					SET $ra_ulutiu_fire_spl(~%SOURCE_RES%~) = 1
				END  // Spells that deal fire damage
				DEFAULT
			END
		END
	END
BUT_ONLY  // One more pass to catch other spells that cast those offending spells
COPY_EXISTING_REGEXP ~^.+\.itm$~ override
	TO_UPPER	SOURCE_RES
	SET	illegal = 0
	SET	ab_size = 0x38
	READ_LONG 0x64 ab_off
	READ_SHORT 0x68 ab_num
	READ_LONG 0x6a fx_off
	FOR (i = 0; i < ab_num; ++i) BEGIN
		READ_SHORT (ab_off + i * ab_size + 0x1e) fx_num
		READ_SHORT (ab_off + i * ab_size + 0x20) fx_idx
		FOR (j = 0; j < fx_num; ++j) BEGIN
			READ_SHORT	(fx_off + (fx_idx + j) * 0x30)	opcode
			READ_LONG	(fx_off + (fx_idx + j) * 0x30 + 0x8)	parameter2
			READ_ASCII	(fx_off + (fx_idx + j) * 0x30 + 0x14)	resource TO_UPPER resource
			PATCH_MATCH opcode WITH
				12 WHEN parameter2 = 0x80000 BEGIN
					SET i = ab_num
					SET j = fx_num
					SET illegal = 1
				END  // Fire Damage
				146 148 232 326 333 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_spl(~%resource%~) BEGIN
					SET i = ab_num
					SET j = fx_num
					SET illegal = 1
				END  // Spells that deal fire damage
				177 248 249 272 283 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_eff(~%resource%~) BEGIN
					SET i = ab_num
					SET j = fx_num
					SET illegal = 1
				END // EFF files that deal fire damage
				333 WHEN VARIABLE_IS_SET $ra_ulutiu_fire_spl(~%SOURCE_RES%B~) BEGIN
					PATCH_IF	~%resource%~ STRING_EQUAL ~~ BEGIN
						SET i = ab_num
						SET j = fx_num
						SET illegal = 1
					END
				END // Opcode 333 uses ('SOURCE'B) as it's resource when left empty
				DEFAULT
			END	
		END
	END
	PATCH_IF	illegal = 1 BEGIN
		LPF	ADD_ITEM_EQEFFECT
			 INT_VAR
				opcode = 319
				target = 1
				parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAUlutRC~)  // replace KITID
				parameter2 = 9
		END
	END
BUT_ONLY

//EoF