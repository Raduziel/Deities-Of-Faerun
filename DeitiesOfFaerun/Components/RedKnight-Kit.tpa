//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						   Holy Strategist of the Red Knight
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RARedK~
    ~RARedK 0 0 0 0 1 1 0 1~
    ~RARedK 0 0 0 0 1 1 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RARedK 3 3 3 13 13 10~
	~RARedK 0 0 0 0 0 0~
	~RARedK 3 3 3 13 13 10~
    ~RARedK 3 3 3 13 13 10~
	~RARedK 0 1 0 0 0 0 0 0 0~
	~RARedK 1 0 0 0 0 0~
	~DeitiesOfFaerun/Tables/RARedK.2da~
	~K_C_H K_C_HE K_C_D K_C_HO K_C_E K_C_HL K_C_G~
	~0x00000000 3~
	~Cl0~
	~PLAT09 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * WAFLAIL HAMM09 *~
	SAY @2901
	SAY @2902
	SAY @2903

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RARedK~
		clascolr = ~198 29 47 46 195~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAREDK~  clabfile = ~RAREDK~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSREDK.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSREDK~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSREDK.itm~ SAY NAME1 @2904 SAY NAME2 @2904 SAY DESC @2905
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RARedK + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~REDKS~
WRITE_ASCII 0x058 ~REDKL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RARedKF.spl~ override

ACTION_IF !FILE_EXISTS_IN_GAME ~RAForeIc.bam~ BEGIN
	
	COPY ~DeitiesOfFaerun/Spl/RAForeIc.bam~ override
	LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@2910) STR_VAR bam_file = RAFOREIC RET iconfore = icon END

END ELSE BEGIN
	OUTER_SPRINT	file ~RAForeIc~
	COPY_EXISTING ~statdesc.2da~	override
		REPLACE_EVALUATE CASE_INSENSITIVE ~[%WNL%^]\(0x[0-9A-F]+\|[0-9]+\)[ %TAB%]+[0-9\-]+[ %TAB%]+%file%[ %TAB%%WNL%$]~ BEGIN	SET	iconfore = (~%MATCH1%~)	END	~%MATCH0%~
	BUT_ONLY
END

//Clairvoyance

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_CLAIRVOYANCE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_CLAIRVOYANCE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAREDK1.spl~
LPM ~SPELL_TO_INNATE~

END                                                  	  
		  
COPY ~DeitiesOfFaerun/Spl/RAREDK2.spl~ override SAY NAME1 @2906 SAY UNIDENTIFIED_DESC @2907              //Divination
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconfore END

COPY ~DeitiesOfFaerun/Spl/RAREDK3.spl~ override SAY NAME1 @2908 SAY UNIDENTIFIED_DESC @2909              //Consequence
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 1 parameter2 = iconfore END

//Mass Invisibility

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_MASS_INVISIBILITY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_MASS_INVISIBILITY~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAREDK4.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

COPY_EXISTING ~RARedK.2da~ ~override/RARedKFC.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAFREDK~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ override
PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	~%animation%~ WITH
				~3A~ ~2A~ BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RARedK~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~REDKS~ ~REDKL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~REDKDIVI~ ~REDKCONS~ BEGIN
COPY ~DeitiesOfFaerun/Spl/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF