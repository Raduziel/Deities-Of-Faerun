//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						         Talon of Malar
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAMala~
    ~RAMala 0 0 0 0 1 1 0 1~
    ~RAMala 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 1 1 1 0 0 1 1 1 0 0 0 0 0 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAMala 13 3 3 3 12 3~
	~RAMala 0 0 0 0 0 0~
	~RAMala 13 3 3 3 12 3~
    ~RAMala 13 3 3 3 12 3~
	~RAMala 0 0 0 0 0 0 0 0 1~
	~RAMala 0 0 0 0 0 1~
	~DeitiesOfFaerun\Tables\RAMala.2da~
	~K_C_H K_C_HO~
	~0x00000000 3~
	~Cl0~
	~LEAT21 * HELM14 * RING09 RING31 CLCK04 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * * HAMM09 *~
	SAY @1801
	SAY @1802
	SAY @1803

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAMala~
		clascolr = ~198 87 198 29 198~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 0 0 0 0 0 1~
END

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAMALA~  clabfile = ~RAMALA~	END

END

COPY "DeitiesOfFaerun\Spl\RAHSMALA.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSMALA.itm" override SAY NAME1 @1806 SAY NAME2 @1806 SAY DESC @1807
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
END

COPY "DeitiesOfFaerun\Itm\MalarClaw\RAMALACS.SPL" override

COPY "DeitiesOfFaerun\Itm\MalarClaw\RAMALA.itm" ~override/RAMALACL.itm~ SAY NAME1 @1804 SAY NAME2 @1804 SAY DESC @1805
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
END

COPY "DeitiesOfFaerun\Spl\RAMALAC.spl" ~override~

COPY "DeitiesOfFaerun\Spl\RAMALAF.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Charmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL "%RA_DireCharmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 3 match_opcode = 267 parameter1 = EVAL "%RA_Dominated%" END

//Beast Claw

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_BEAST_CLAW[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_BEAST_CLAW~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMALA1.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_SPELL ~DeitiesOfFaerun/Lib/Spheres/Import/BeastClaw/SPPR219.spl~ 1 2 CLERIC_BEAST_CLAW
SAY NAME1 @9999128 SAY NAME2 @9999128 SAY UNIDENTIFIED_DESC @9999129 SAY DESC @9999129
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT       STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/BeastClaw/SPPR219B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/BeastClaw/SPPR219C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/BeastClaw/BCLAW.ITM~	~override~
SAY NAME1 @9999128 SAY NAME2 @9999128 SAY UNIDENTIFIED_DESC @9999128 SAY DESC @9999128

COPY_EXISTING	~%spell_res%.spl~	~override/RAMALA1.spl~
LPM ~SPELL_TO_INNATE~

END

//Animal Rage

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_ANIMAL_RAGE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_ANIMAL_RAGE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMALA2.spl~
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMALA2~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%B.spl~	~override/RAMALA2B.spl~
IF_EXISTS

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT	INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMALA2~	END

END ELSE BEGIN

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@9999130) STR_VAR bam_file = RARAGEIC RET iconrage = icon END
ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/AnimalRage/SPPR522.spl~ 1 5 CLERIC_ANIMAL_RAGE
SAY NAME1 @9999130 SAY NAME2 @9999130 SAY UNIDENTIFIED_DESC @9999131 SAY DESC @9999131
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT	   STR_VAR silent = 1  match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END
LPF	ALTER_EFFECT	   STR_VAR silent = 1  match_resource = EVAL ~%SOURCE_RES%B~  resource = EVAL ~%spell_res%B~	END
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 142 match_parameter2 = 173   parameter2 = iconrage END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/AnimalRage/SPPR522B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/AnimalRage/SPPR522C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/AnimalRage/SPPR522B.spl~	~override\%spell_res%B.spl~
LPF ALTER_EFFECT 	   INT_VAR match_opcode = 139 match_parameter1 = 37605 parameter1 = EVAL "%RA_Berserk%" END
LPF	ALTER_EFFECT	   STR_VAR silent = 1         match_resource = EVAL ~SPPR522~	      resource = EVAL ~%spell_res%~	    END
LPF	ALTER_EFFECT	   STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~    resource = EVAL ~%spell_res%B~	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMALA2.spl~
LPF	CLONE_EFFECT INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMALA2~	END
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMALA2~ END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%B.spl~	~override/RAMALA2B.spl~
IF_EXISTS

COPY_EXISTING	~%spell_res%.spl~	~override~
LPF	CLONE_EFFECT INT_VAR	match_opcode = 321 silent = 1 STR_VAR match_resource = EVAL ~%spell_res%~ resource = EVAL ~RAMALA2~	END
LPM ~SPELL_TO_INNATE~

END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 98 99 103 104 105 106 107 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@1802)
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "3A" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAMala~) parameter2 = 9 special = RESOLVE_STR_REF (@1802) END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~MALAS~ ~MALAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~RAMALACL~ ~RAMALACS~ BEGIN
COPY ~DeitiesOfFaerun\Itm\MalarClaw\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun\Itm\MalarClaw\MOS%original_base_index%.pvrz~
    END
  END
END

//EoF