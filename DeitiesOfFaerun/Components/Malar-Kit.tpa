//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Talon of Malar
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAMala~
    ~RAMala 0 0 0 0 1 1 0 1~
    ~RAMala 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 1 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAMala 13 3 3 3 12 3~
	~RAMala 0 0 0 0 0 0~
	~RAMala 13 3 3 3 12 3~
    ~RAMala 13 3 3 3 12 3~
	~RAMala 0 0 0 0 0 0 0 0 1~
	~RAMala 1 1 1 1 1 1~
	~DeitiesOfFaerun\Tables\RAMala.2da~
	~K_C_H K_C_HO~
	~0x04000000 3~
	~Cl0~
	~LEAT21 * HELM14 * RING09 RING31 CLCK04 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * MISC75 HAMM09 *~
	SAY @1801
	SAY @1802
	SAY @1803

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAMala~
		clascolr = ~68 71 105 222 68~ //Rever
		clswpbon = ~1 0 3~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 0
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 1
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~x~
	s_Death 		= ~x~
	s_Benediction 	= ~x~
	s_Destruction 	= ~major~
	s_Protection 	= ~x~
	s_War 			= ~major~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~major~
	s_Vigor 		= ~major~
	s_Affliction 	= ~minor~
	s_Animal 		= ~major~
	s_Plant 		= ~minor~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAHSMALA.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSMALA.itm" override SAY NAME1 @1810 SAY NAME2 @1810 SAY DESC @1811
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
END
		  
COPY "DeitiesOfFaerun\Itm\HolySymbols\RAMalaCS.spl" override		  
		  
COPY "DeitiesOfFaerun\Itm\HolySymbols\RAMalaCI.itm" override SAY NAME1 @1804 SAY NAME2 @1804 SAY DESC @1805
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
END
		  
COPY "DeitiesOfFaerun\Spl\RAMALAFE.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Charmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 2 match_opcode = 267 parameter1 = EVAL "%RA_DireCharmed%" END
LPF ALTER_EFFECT INT_VAR match_parameter1 = 3 match_opcode = 267 parameter1 = EVAL "%RA_Dominated%" END


ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN
		  
COPY "DeitiesOfFaerun\Spl\Import\Malar\Complement" override

COPY "DeitiesOfFaerun\Spl\Import\Malar\BCLAW.ITM" override SAY NAME1 @1806 SAY NAME2 @1806 SAY DESC @1807

COPY "DeitiesOfFaerun\Spl\Import\Malar\RAPR219.SPL" ~override/RAMALA1~  SAY NAME1 @1806 SAY UNIDENTIFIED_DESC @1807
LPM SPELL_TO_INNATE

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@1808) STR_VAR bam_file = ANRAGEIC RET icon END
COPY "DeitiesOfFaerun\Spl\Import\Malar\RAPR522.SPL" ~override/RAMALA2~  SAY NAME1 @1808 SAY UNIDENTIFIED_DESC @1809
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 173 parameter2 = icon END
LPM SPELL_TO_INNATE

END

ACTION_IF GAME_IS ~iwdee~ BEGIN

COPY_EXISTING ~SPPR219.spl~ ~override/RAMALA1~
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR522.spl~ ~override/RAMALA2~
LPM SPELL_TO_INNATE

END

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMala + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@1802)
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "4A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAMala~) parameter2 = 9 special = RESOLVE_STR_REF (@1802) END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAMala~) parameter2 = 9 special = RESOLVE_STR_REF (@1802) END
	END
END
END
BUT_ONLY

ACTION_FOR_EACH file IN ~MALAS~ ~MALAL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~RAMALACL~ ~RAMALACS~ BEGIN
COPY ~DeitiesOfFaerun\Itm\MalarClaw\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun\Itm\MalarClaw\MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF