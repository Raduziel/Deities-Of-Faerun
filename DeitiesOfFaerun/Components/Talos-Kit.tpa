//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						         Stormcaller of Talos
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATalo~
    ~RATalo 0 0 0 1 1 1 0 1~
    ~RATalo 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 1 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATalo 13 3 3 3 14 3~
	~RATalo 0 0 0 0 0 0~
	~RATalo 13 3 3 3 14 3~
    ~RATalo 13 3 3 3 14 3~
	~RATalo 0 0 0 0 0 0 0 0 1~
	~RATalo 1 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RATalo.2da~
	~K_C_H K_C_HE K_C_D K_C_HO K_C_E K_C_HL K_C_G~
	~0x00000000 3~
	~Cl0~
	~PLAT18 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SPER08 HAMM09 *~
	SAY @901
	SAY @902
	SAY @903

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATalo~
		clascolr = ~213 97 136 183 190~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RATALO~  clabfile = ~RATALO~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSTALO.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSTALO~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSTALO.itm~ SAY NAME1 @904 SAY NAME2 @904 SAY DESC @905
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RATalo + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~TALOS~
WRITE_ASCII 0x058 ~TALOL~

//Spells

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_PROTECTION_FROM_ELECTRICITY[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_PROTECTION_FROM_ELECTRICITY~	RET	spell_res	END

COPY_EXISTING ~%spell_res%.spl~ ~override/RATALOF.spl~
LPM ~SPELL_TO_FEATURE~

END

//Lightning Bolt
		  
ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_LIGHTNING_BOLT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_LIGHTNING_BOLT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATALO1.spl~
LPM ~SPELL_TO_INNATE~

END

//Call Lightning

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CALL_LIGHTNING[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CALL_LIGHTNING~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATALO2.spl~
LPM ~SPELL_TO_INNATE~

END

//Shocking Grasp

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_SHOCKING_GRASP[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_SHOCKING_GRASP~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATALO3.spl~
LPM ~SPELL_TO_INNATE~

END

//Earthquake

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_EARTHQUAKE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_EARTHQUAKE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RATALO4.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

COPY_EXISTING ~RATalo.2da~ ~override/RATaloFC.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAFTALO~
PRETTY_PRINT_2DA

COPY_EXISTING ~RATalo.2da~ ~override/RATaloCM.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAMTALO~
SET_2DA_ENTRY 6  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATalo~)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~TALOS~ ~TALOL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//Hide the original kit

COPY_EXISTING
~K_C_H.2DA~  override
~K_C_HO.2DA~ override 
~K_C_HE.2DA~ override
~K_C_HL.2DA~ override
~K_C_E.2DA~  override
~K_C_D.2DA~  override
~K_C_G.2DA~  override
	READ_2DA_ENTRIES_NOW ~READ_K_C_H~ 2
	FOR (i = READ_K_C_H - 1; i >= 0; --i) BEGIN
		READ_2DA_ENTRY_FORMER ~READ_K_C_H~ i 1 kit
		PATCH_MATCH kit WITH
			19 BEGIN
				REMOVE_2DA_ROW i 2
			END
			DEFAULT
		END
	END
BUT_ONLY

//Hide the kit from Dual Classing

  COPY_EXISTING ~DUALCLAS.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 0 ~kit_name~
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~TALOS~) BEGIN
        SET talos_row = %row%
      END
    END
    FOR (col = 1; col < 7; ++col) BEGIN
      PATCH_IF IS_AN_INT %talos_row% BEGIN
        SET_2DA_ENTRY %talos_row% col cols 0
      END
    END
  BUT_ONLY

//EoF