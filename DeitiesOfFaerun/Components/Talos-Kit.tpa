//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						         Stormcaller of Talos
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATalo~
    ~RATalo 0 0 0 1 1 1 0 1~
    ~RATalo 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 1 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATalo 13 3 3 3 14 3~
	~RATalo 0 0 0 0 0 0~
	~RATalo 13 3 3 3 14 3~
    ~RATalo 13 3 3 3 14 3~
	~RATalo 0 0 0 0 0 0 0 0 1~
	~RATalo 1 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RATalo.2da~
	~K_C_H K_C_HE K_C_D K_C_HO K_C_E K_C_HL K_C_G~
	~0x04000000 3~
	~Cl0~
	~PLAT18 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SPER08 HAMM09 *~
	SAY @901
	SAY @902
	SAY @903

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATalo~
		clascolr = ~205 97 142 41 96~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 1
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

	STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~minor~
	s_Benediction 	= ~minor~
	s_Destruction 	= ~major~
	s_Protection 	= ~x~
	s_War 			= ~major~
	s_Exploration   = ~x~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~major~
	s_Vigor 		= ~x~
	s_Affliction 	= ~minor~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~major~
	s_Air 			= ~major~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY_EXISTING ~SPWI512.spl~ ~override/RATALOF.spl~
WRITE_SHORT 0x1C 4                                                    
WRITE_LONG  0x34 1                                                         
WRITE_LONG  0x22 00
LPF ALTER_SPELL_HEADER INT_VAR projectile = 0 target = 5 range = 1 speed = 0 location = 0 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 9   END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 50  END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 139 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 141 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END
LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 215 END
LPF ALTER_EFFECT INT_VAR target = 2 timing = 9 duration = 0 resist_dispel = 0 END

COPY "DeitiesOfFaerun\Spl\RAHSTALO.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSTALO.itm" override SAY NAME1 @904 SAY NAME2 @904 SAY DESC @905
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATalo~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY_EXISTING ~SPWI308.spl~ ~override/RATALO1.spl~    //Lightning Bolt
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR302.spl~ ~override/RATALO2.spl~    //Call Lightning
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPWI115.spl~ ~override/RATALO3.spl~    //Shocking Grasp
LPM SPELL_TO_INNATE

COPY_EXISTING ~SPPR720.spl~ ~override/RATALO4.spl~    //Earthquake
LPM SPELL_TO_INNATE

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATalo~)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@902)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~TALOS~ ~TALOL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//Hide the original kit

COPY_EXISTING
~K_C_H.2DA~  override
~K_C_HO.2DA~ override 
~K_C_HE.2DA~ override
~K_C_HL.2DA~ override
~K_C_E.2DA~  override
~K_C_D.2DA~  override
~K_C_G.2DA~  override
	READ_2DA_ENTRIES_NOW ~READ_K_C_H~ 2
	FOR (i = READ_K_C_H - 1; i >= 0; --i) BEGIN
		READ_2DA_ENTRY_FORMER ~READ_K_C_H~ i 1 kit
		PATCH_MATCH kit WITH
			19 BEGIN
				REMOVE_2DA_ROW i 2
			END
			DEFAULT
		END
	END
BUT_ONLY

//Hide the kit from Dual Classing

COPY_EXISTING ~DUALCLAS.2DA~ ~override~
SET_2DA_ENTRY_LATER ~RADUALCLAS~ 47 1 ~0~
SET_2DA_ENTRY_LATER ~RADUALCLAS~ 47 3 ~0~
SET_2DA_ENTRY_LATER ~RADUALCLAS~ 47 4 ~0~
SET_2DA_ENTRY_LATER ~RADUALCLAS~ 47 6 ~0~
SET_2DA_ENTRIES_NOW ~RADUALCLAS~ 1
PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES

//EoF