//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					    Silentwalker of Mask - Cleric/Thief
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAMaskCT~
    ~RAMaskCT 0 0 0 0 1 1 0 0~
    ~RAMaskCT 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1 1 1 0 0 1 1 1 1 0 0 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAMaskCT 3 14 3 3 14 3~
	~RAMaskCT 0 0 0 0 0 0~
	~RAMaskCT 14 3 3 3 12 3~
    ~RAMaskCT 14 3 3 3 12 3~
	~RAMaskCT 0 0 0 0 0 1 0 0 0~
	~RAMaskCT 0 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAMaskCT.2da~
	~K_CT_H   K_CT_HE   K_CT_E   K_CT_G   K_CT_HO   K_CT_HL   K_CT_D~
	~0x00000000 15~
	~CT0~
	~LEAT17 * HELM04 * RING05 RING39 CLCK03 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * DAGG11 WAFLAIL *~
	SAY @1601
	SAY @1602
	SAY @1603

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAMaskCT~
		clascolr = ~206 107 138 107 104~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAMaskCT~
      kit_clab = ~RAMaskCT~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RAMaskCT%	classID = 15 STR_VAR	kitname = ~RAMASK~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RATMASK.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RACTMASK~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RACTMASK.itm~ SAY NAME1 @1604 SAY NAME2 @1604 SAY DESC @1606
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAMaskCT + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~MASKS~
WRITE_ASCII 0x058 ~MASKL~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 98 99 104 105 106 BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMaskCT + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "3A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAMaskCT~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//EoF