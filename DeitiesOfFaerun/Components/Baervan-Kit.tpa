//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Fastpaw of Baervan
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RABaer~
    ~RABaer 0 1 1 1 1 0 1 1~
    ~RABaer 0 1 1 1 1 0 1 1 0 0 0 1 0 0 0 1 0 1 1 0 0 0 1 1 0 1 0 1 1 0 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RABaer 3 3 10 3 9 3~
	~RABaer 0 0 0 0 0 0~
	~RABaer 3 3 10 3 9 3~
    ~RABaer 3 3 10 3 9 3~
	~RABaer 0 0 0 1 0 0 1 0 0~
	~RABaer 0 0 1 0 0 1~
	~DeitiesOfFaerun\Tables\RABaer.2da~
	~K_C_G~
	~0x00000000 3~
	~Cl0~
	~LEAT19 * HELM16 * RING29 RING31 CLCK27 BOOT04 AMUL27 BRAC10 BELT03 * * * * * * BLUN24 WASLING *~
	SAY @701
	SAY @702
	SAY @703

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RABaer~
		clascolr = ~251 43 8 91 9~
		clswpbon = ~0 0 3~
		clsrcreq = ~0 0 0 0 0 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RABAER~  clabfile = ~RABAER~	END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAHSBAER.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSBAER~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAHSBAER.itm~ SAY NAME1 @704 SAY NAME2 @704 SAY DESC @705
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RABaer + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~BAERS~
WRITE_ASCII 0x058 ~BAERL~

//Spells

COPY "DeitiesOfFaerun\Spl\RABAERF.spl" override
LPF ADD_SPELL_EFFECT INT_VAR opcode = 83   parameter2 = 319 target = 2 timing = 9 END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 83   parameter2 = 259 target = 2 timing = 9 END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 83   parameter2 = 243 target = 2 timing = 9 END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~BDBOW06~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~BDIMPALE~ END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~BDSHAMB~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~SPIN688~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~SPPR105~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~SPWM111~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~GASP~     END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~HOLDFST~  END
LPF ADD_SPELL_EFFECT INT_VAR opcode = 206  target = 2 timing = 9     STR_VAR resource = ~TALONGF~  END

//Animal Summoning III

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_ANIMAL_SUMMONING_3[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_ANIMAL_SUMMONING_3~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABAER1.spl~
LPM ~SPELL_TO_INNATE~

END

//Barkskin

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_BARKSKIN[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_BARKSKIN~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABAER2.spl~
LPM ~SPELL_TO_INNATE~

END

//Changestaff

COPY "DeitiesOfFaerun\Spl\RABAER3.spl" override SAY NAME1 @706 SAY UNIDENTIFIED_DESC  @707       
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 16; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 15; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT	INT_VAR	header = (i - 15) match_duration = 60 duration = (60 * i) END
	END


COPY "DeitiesOfFaerun\Cre\Changestaff\Complement"   override                                            //Treant (Animation) - Thanks for teaching me how to do this, Kjeron!

COPY "DeitiesOfFaerun\Cre\Changestaff\RATREANT.cre" override SAY NAME1 @708 SAY NAME2 @708              //Treant (Creature)

COPY "DeitiesOfFaerun\Cre\Changestaff\RAPlant.itm" override                                             //Treant (Ring)
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 1 parameter1 = EVAL "%RA_Stun%"      END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 2 parameter1 = EVAL "%RA_Stunned%"   END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 3 parameter1 = EVAL "%RA_Poison%"    END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 4 parameter1 = EVAL "%RA_Poisoned%"  END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 5 parameter1 = EVAL "%RA_Paralyzed%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 6 parameter1 = EVAL "%RA_Held%"      END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 7 parameter1 = EVAL "%RA_Sleep%"     END
LPF ALTER_EFFECT INT_VAR match_opcode = 267 match_parameter1 = 8 parameter1 = EVAL "%RA_Bleeding%"  END

//Call Woodland Beings

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_CALL_WOODLAND_BEINGS[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_CALL_WOODLAND_BEINGS~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RABAER4.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

COPY_EXISTING ~RABaer.2da~ ~override/RABaerRC.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RARBAER~
PRETTY_PRINT_2DA

COPY_EXISTING ~RABaer.2da~ ~override/RABaerCM.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAMBAER~
SET_2DA_ENTRY 6  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	SET match = 0
	PATCH_MATCH proficiency WITH 
		89 90 91 93 94 95 97 99 100 101 104 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RABaer + 0x4000)
			  parameter2 = 9
			  timing = 2
		END
	SET match = 1
		END
		DEFAULT
		END
	PATCH_IF (SOURCE_SIZE > 0x71) AND (match = 0) BEGIN
		READ_BYTE 0x18 flags1
		PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
			READ_ASCII	0x22 animation (2)
			TO_UPPER animation
			PATCH_MATCH	"%animation%" WITH
				"4A" "3A" "D1" "D2" "D3" "D4" "C0" "C1" "C2" "C3" "C4" "C5" "C6" BEGIN
					LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RABaer~) parameter2 = 9 END
				END
				DEFAULT
			END
		END
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~BAERS~ ~BAERL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~BAERCHAN~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF