//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Silverstar of Sel√ªne
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RASelu~
    ~RASelu 0 0 0 0 1 1 0 1~
    ~RASelu 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RASelu 3 3 12 3 14 3~
	~RASelu 0 0 0 0 0 0~
	~RASelu 3 3 12 3 14 3~
    ~RASelu 3 3 12 3 14 3~
	~RASelu 0 0 0 0 0 0 1 0 0~
	~RASelu 1 0 1 0 0 0~
	~DeitiesOfFaerun/Tables/RASelu.2da~
	~K_C_H K_C_HO K_C_HE K_C_D K_C_HL K_C_G K_C_E~
	~0x00000000 3~
	~Cl0~
	~PLAT16 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 HAMM09 *~
	SAY @2001
	SAY @2002
	SAY @2003

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RASelu~
		clascolr = ~79 222 123 97 102~
		clsrcreq = ~1 1 1 1 1 1 1~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RASELU~  clabfile = ~RASELU~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSSELU.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSSELU~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSSELU.itm~ SAY NAME1 @2004 SAY NAME2 @2004 SAY DESC @2005
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RASelu + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~SELUS~
WRITE_ASCII 0x058 ~SELUL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RASELUF.spl~ override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL ~%RA_Blinded%~ END

//Moonbeam

COPY ~DeitiesOfFaerun/Spl/RASELU1.spl~ override SAY NAME1 @2006 SAY UNIDENTIFIED_DESC @2007
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL ~%RA_Blinded%~ END
	READ_LONG	0x64	ab_off
	READ_SHORT	0x68	ab_num
	READ_LONG	0x6a	fx_off
	READ_ASCII	ab_off	ability (0x28)
	READ_SHORT	(ab_off + 0x1e) fx_num
	READ_SHORT	(ab_off + 0x20) fx_idx
	READ_ASCII	(fx_off + fx_idx * 0x30) effects (fx_num * 0x30)
	FOR (i = 2; i < 51; ++i)	BEGIN
		INSERT_BYTES	(ab_off + ab_num * 0x28) 0x28	SET	fx_off += 0x28
		WRITE_ASCIIE	(ab_off + ab_num * 0x28) ~%ability%~ (0x28)
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x10)	i
		WRITE_SHORT	(ab_off + ab_num * 0x28 + 0x20)	(fx_idx + fx_num)
		INSERT_BYTES	(fx_off + fx_idx * 0x30) (fx_num * 0x30)
		WRITE_ASCIIE	(fx_off + fx_idx * 0x30) ~%effects%~ (fx_num * 0x30)
		SET	fx_idx += fx_num
		SET	ab_num += 1
	END
	WRITE_SHORT	0x68	ab_num
	WRITE_LONG	0x6a	fx_off
	FOR (i = 1; i < 51; ++i)	BEGIN
		LPF	ALTER_EFFECT INT_VAR header = (i - 1) match_duration = 6 duration = (6 * i) END
	END

//Moonblade

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_MOONBLADE[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_MOONBLADE~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASELU2.spl~
LPM ~SPELL_TO_INNATE~

END ELSE BEGIN

ADD_SPELL ~DeitiesOfFaerun/Lib/Spheres/Import/Moonblade/SPPR322.spl~ 1 3 CLERIC_MOONBLADE
SAY NAME1 @9999106 SAY NAME2 @9999106 SAY UNIDENTIFIED_DESC @9999107 SAY DESC @9999107
TEXT_SPRINT	spell_res	~%DEST_RES%~	TO_UPPER	spell_res
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF	ALTER_EFFECT       STR_VAR silent = 1         match_resource = EVAL ~%SOURCE_RES%~	  resource = EVAL ~%spell_res%~	    END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Moonblade/SPPR322B.BAM~	~override/%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Moonblade/SPPR322C.BAM~	~override/%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Moonblade/MOONBLA.ITM~	override
SAY NAME1 @9999106 SAY NAME2 @9999106 SAY UNIDENTIFIED_DESC @9999106 SAY DESC @9999106

COPY_EXISTING	~%spell_res%.spl~	~override/RASELU2.spl~
LPM ~SPELL_TO_INNATE~

END

//True Seeing

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_TRUE_SIGHT[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_TRUE_SIGHT~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RASELU3.spl~
LPF	ALTER_EFFECT STR_VAR silent = 1 match_resource = EVAL ~%spell_res%D~ resource = EVAL ~RASELU3D~	END
LPM ~SPELL_TO_INNATE~

COPY_EXISTING	~%spell_res%D.spl~	~override/RASELU3D.spl~

END
		  
ADD_PROJECTILE ~DeitiesOfFaerun/Spl/RASELU4.PRO~

COPY ~DeitiesOfFaerun/Spl/UB!SSta1.bam~ override

//Shooting Star

COPY ~DeitiesOfFaerun/Spl/RASELU4.spl~ override SAY NAME1 @2008 SAY UNIDENTIFIED_DESC @2009
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RASELU4% END

//Multiclass CLAB

COPY_EXISTING ~RASelu.2da~ ~override/RASeluFC.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAFSELU~
PRETTY_PRINT_2DA

COPY_EXISTING ~RASelu.2da~ ~override/RASeluCM.2da~
SET_2DA_ENTRY 6 25 50 ~AP_RAMSELU~
SET_2DA_ENTRY 6  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RASelu + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~SELUS~ ~SELUL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SELUSHST~ ~SELUMOBE~ BEGIN
COPY ~DeitiesOfFaerun/Spl/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF