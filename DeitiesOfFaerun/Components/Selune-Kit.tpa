//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Silverstar of SelÃ»ne
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RASelu~
    ~RASelu 0 0 0 0 1 1 0 1~
    ~RASelu 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RASelu 3 3 12 3 14 3~
	~RASelu 0 0 0 0 0 0~
	~RASelu 3 3 12 3 14 3~
    ~RASelu 3 3 12 3 14 3~
	~RASelu 0 0 0 0 0 0 1 0 0~
	~RASelu 1 0 1 0 0 0~
	~DeitiesOfFaerun\Tables\RASelu.2da~
	~K_C_H K_C_HO K_C_HE K_C_D K_C_HL K_C_G K_C_E~
	~0x04000000 3~
	~Cl0~
	~PLAT16 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 HAMM09 *~
	SAY @2001
	SAY @2002
	SAY @2003

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RASelu~
		clascolr = ~79 222 121 95 79~
		clswpbon = ~0 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

 COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 0
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~major~
	s_Death 		= ~x~
	s_Benediction 	= ~major~
	s_Destruction 	= ~x~
	s_Protection 	= ~major~
	s_War 			= ~x~
	s_Exploration   = ~minor~
	s_Knowledge 	= ~minor~
	s_Deception 	= ~x~
	s_Thought 		= ~minor~
	s_Dread 		= ~x~
	s_Vigor 		= ~minor~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~major~
	s_Light 		= ~major~
	s_Shadow 		= ~x~
	s_Magic 		= ~major~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAHSSELU.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHSSELU.itm" override SAY NAME1 @2010 SAY NAME2 @2010 SAY DESC @2011
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RASelu + 0x4000)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END

COPY ~DeitiesOfFaerun\Spl\RASELUF.spl~ override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 267 parameter1 = EVAL "%RA_Blinded%" END

COPY ~DeitiesOfFaerun\Spl\RASELU1.spl~ override SAY NAME1 @2004 SAY UNIDENTIFIED_DESC @2005
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = EVAL "%RA_Blinded%" END

ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN

COPY ~DeitiesOfFaerun\Spl\Import\Selune\Complement~ override

COPY ~DeitiesOfFaerun\Spl\Import\Selune\MOONBLA.itm~ override SAY NAME1 @2006 SAY NAME2 @2006 SAY DESC @2006

COPY ~DeitiesOfFaerun\Spl\Import\Selune\RAPR322.spl~ ~override/RASELU2.spl~ SAY NAME1 @2006 SAY UNIDENTIFIED_DESC @2007
LPM SPELL_TO_INNATE

END		  

ACTION_IF GAME_IS ~iwdee~ BEGIN

COPY_EXISTING ~SPPR322.spl~ ~override\RASELU2.spl~
LPM SPELL_TO_INNATE

END

COPY_EXISTING ~SPPR505.spl~ ~override\RASELU3.spl~
LPM SPELL_TO_INNATE
		  
ADD_PROJECTILE ~DeitiesOfFaerun\Spl\RASELU4.PRO~

COPY "DeitiesOfFaerun\Spl\UB!SSta1.bam" override

COPY "DeitiesOfFaerun\Spl\RASELU4.spl" override SAY NAME1 @2008 SAY UNIDENTIFIED_DESC @2009
LPF ALTER_SPELL_HEADER INT_VAR projectile = %RASELU4% END


COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RASelu + 0x4000)
			  parameter2 = 9
			  timing = 2
			  special = RESOLVE_STR_REF (@2002)
		  END
		END
		DEFAULT
	END
BUT_ONLY

ACTION_FOR_EACH file IN ~SELUS~ ~SELUL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

ACTION_FOR_EACH file IN ~SELUSHST~ ~SELUMOBE~ BEGIN
COPY ~DeitiesOfFaerun\Spl\BAM\%file%.bam~ ~override~
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Spl/BAM/MOS%original_base_index%.pvrz~
    END
  END
END	

//EoF