//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						Holy Justice of Tyr - Fighter/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RATyrFC~
    ~RATyrFC 1 0 0 0 1 1 0 1~
    ~RATyrFC 0 0 0 0 1 1 0 1 2 2 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RATyrFC 9 3 3 3 9 3~
	~RATyrFC 0 0 0 0 0 0~
	~RATyrFC 9 3 3 3 9 3~
    ~RATyrFC 9 3 3 3 9 3~
	~RATyrFC 1 0 0 0 0 0 0 0 0~
	~RATyrFC 0 0 0 0 0 0~
	~override\RATyrFC.2da~
	~K_FC_H K_FC_HE K_FC_D K_FC_HO K_FC_E K_FC_HL K_FC_G~
	~0x00000000 8~
	~FC0~
	~PLAT15 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * SW1H31 HAMM09 *~
	SAY @1101
	SAY @1102
	SAY @1103

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RATyrFC~
		clascolr = ~205 96 140 164 104~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RATyrFC~
      kit_clab = ~RATyrFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

LAF	BUILD_SPHERE_ACCESS	INT_VAR	kitid = %RATyrFC%	classID = 8 STR_VAR	kitname = ~RATYR~  END

END

//Holy Symbol

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.spl" ~override/RAFTYR.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAFCTYR~ END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAHS.itm" ~override/RAFCTYR.itm~ SAY NAME1 @1104 SAY NAME2 @1104 SAY DESC @1105
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RATyrFC + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~TYRS~
WRITE_ASCII 0x058 ~TYRL~

//Restrictions

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RATyrFC~)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//EoF