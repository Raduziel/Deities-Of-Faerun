//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						    Watcher of Helm - Fighter/Cleric
//__________________________________________________________________________________
//__________________________________________________________________________________

INCLUDE ~DeitiesOfFaerun\Lib\Multiclass\qd_multiclass.tpa~

ADD_KIT ~RAHelmFC~
    ~RAHelmFC 0 0 0 0 1 1 0 1~
    ~RAHelmFC 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAHelmFC 14 3 3 3 13 3~
	~RAHelmFC 0 0 0 0 0 0~
	~RAHelmFC 14 3 3 3 13 3~
    ~RAHelmFC 14 3 3 3 13 3~
	~RAHelmFC 0 1 0 0 0 0 0 0 0~
	~RAHelmFC 0 0 0 0 0 0~
	~DeitiesOfFaerun\Tables\RAHelmFC.2da~
	~K_FC_H K_FC_HE K_FC_D K_FC_HO K_FC_E K_FC_HL K_FC_G~
	~0x04000000 8~
	~FC0~
	~PLAT09 SHLD23 HELM07 * RING09 RING31 CLCK20 BOOT04 AMUL27 BRAC10 BELT04 * * * * * * WAFLAIL HAMM09 *~
	SAY @801
	SAY @802
	SAY @803

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAHelmFC~
		clascolr = ~147 238 127 238 170~
		clswpbon = ~1 0 3~
		clsrcreq = ~1 1 1 1 1 1 1~
END

LAF qd_multiclass
  STR_VAR 
      kit_name = ~RAHelmFC~
      kit_clab = ~RAHelmFC~
      base_class = ~P~
	  mc_dir = ~DeitiesOfFaerun\Lib\Multiclass~
END

ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
 BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
	INT_VAR
	u_leather_armor 	= 0
	u_chain_armor 		= 0
	u_plate_armor 		= 1
	u_club_staff	 	= 1
	u_hammers	 		= 1
	u_maces 	 		= 1
	u_flails 			= 1
	u_axes 				= 0
	u_daggers 			= 0
	u_short_swords		= 0
	u_long_swords		= 0
	u_scimitars 		= 0
	u_katanas 			= 0
	u_bastard_swords 	= 0
	u_2hand_swords 		= 0
	u_halberds 			= 0
	u_spears 	 		= 0
	u_darts 			= 0
	u_slings	 		= 1
	u_bows 				= 0
	u_crossbows 		= 0

	STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~cleric~	 
	s_Life 			= ~minor~
	s_Death 		= ~x~
	s_Benediction 	= ~major~
	s_Destruction 	= ~x~
	s_Protection 	= ~major~
	s_War 			= ~major~
	s_Exploration   = ~minor~
	s_Knowledge 	= ~major~
	s_Deception 	= ~x~
	s_Thought 		= ~minor~
	s_Dread 		= ~x~
	s_Vigor 		= ~major~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END

COPY "DeitiesOfFaerun\Spl\RAFHELM.spl" override
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@3) END

COPY "DeitiesOfFaerun\Itm\HolySymbols\RAFCHELM.itm" override SAY NAME1 @811 SAY NAME2 @811 SAY DESC @812
LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAHelmFC~)
			  parameter2 = 9
			  timing = 2
			  power = 1
		  END
COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAHelmFC~)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE 0x18 flags1
    PATCH_IF ((flags1 & BIT3) = BIT3) BEGIN
	READ_ASCII	0x22 animation (2)
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "3A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAHelmFC~) parameter2 = 9 END
	END
	PATCH_IF ("%animation%" STRING_EQUAL_CASE "2A") BEGIN
	  LPF ~ADD_ITEM_EQEFFECT~ INT_VAR opcode = 319 target = 1 timing = 2 parameter1 = IDS_OF_SYMBOL (~Kit~ ~RAHelmFC~) parameter2 = 9 END
	END
END
END
BUT_ONLY

//EoF