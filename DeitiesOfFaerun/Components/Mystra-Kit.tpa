//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						      Dweomerkeeper of Mystra
//__________________________________________________________________________________
//__________________________________________________________________________________

ADD_KIT ~RAMyst~
    ~RAMyst 0 0 0 0 1 1 0 1~
    ~RAMyst 0 0 0 0 1 1 0 1 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 0 0 0 1 1 1 1 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	~RAMyst 3 3 3 14 12 3~
	~RAMyst 0 0 0 0 0 0~
	~RAMyst 3 3 3 14 12 3~
    ~RAMyst 3 3 3 14 12 3~
	~RAMyst 0 1 0 1 0 0 0 1 0~
	~RAMyst 0 0 1 0 0 0~
	~DeitiesOfFaerun/Tables/RAMyst.2da~
	~K_C_H K_C_HE K_C_E K_C_G~
	~0x00000000 3~
	~Cl0~
	~CHAN18 * HELM07 * RING07 RING33 CLCK05 BOOT04 AMUL27 BRAC10 BELT06 * * * * * * STAF10 HAMM09 *~
	SAY @1401
	SAY @1402
	SAY @1403

LAF fl#add_kit_ee
	STR_VAR
		kit_name = ~RAMyst~
		clascolr = ~57 79 124 17 18~
		clsrcreq = ~1 1 1 0 0 1 0~
END

//Sphere

ACTION_IF FILE_EXISTS_IN_GAME ~RASPHERE.marker~ BEGIN

	LAF	BUILD_SPHERE_ACCESS	STR_VAR	kitname = ~RAMYST~  clabfile = ~RAMYST~	END

END

//Holy Symbol

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.spl~ ~override/RAHSMYST.spl~
LPF ALTER_EFFECT INT_VAR match_parameter1 = 1 match_opcode = 139 parameter1 = RESOLVE_STR_REF (@9999903) END
LPF ALTER_EFFECT INT_VAR match_opcode = 122   STR_VAR resource = ~RAHSMYST~ END

COPY ~DeitiesOfFaerun/Itm/HolySymbols/RAHS.itm~ ~override/RAHSMYST.itm~ SAY NAME1 @1404 SAY NAME2 @1404 SAY DESC @1405
LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = (RAMyst + 0x4000) parameter2 = 9 timing = 2 power = 1 END
WRITE_ASCII 0x03a ~MYSTS~
WRITE_ASCII 0x058 ~MYSTL~

//Spells

COPY ~DeitiesOfFaerun/Spl/RAMYSTF.spl~ override
PATCH_FOR_EACH label IN
	~WILD_MAGIC_AREA~
	~DEAD_MAGIC_AREA~
	~WILD_MAGIC_ZONE~
	~ANTI_MAGIC_ZONE~
	~CLERIC_MISCAST_MAGIC~
	~WIZARD_ANTIMAGIC_SHELL~
	~BEHOLDER_ANTIMAGIC_RAY~
	~INNATE_BEHOLDER_ANTIMAGIC_RAY~
	~HIVE_MOTHER_ANTIMAGIC_RAY~
	~TRAP_MISCAST_MAGIC~
	~WISH_SPELL_FAILURE~
BEGIN
	PATCH_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]%label%[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
		LPF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = EVAL ~%label%~	RET	spell_res	END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 0 resist_dispel = 0 timing = 9 STR_VAR resource = EVAL ~%spell_res%~ END
	END
END

//Immunity 1: Remove Magic

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_DISPEL_MAGIC[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_DISPEL_MAGIC~	RET	spell_res	END

COPY_EXISTING	~RAMYSTF.spl~	override
LPF ADD_SPELL_EFFECT INT_VAR header = 3 opcode = 206 target = 2 power = 0 resist_dispel = 0 timing = 9 STR_VAR resource = EVAL ~%spell_res%~ END

END

//Immunity 2: Secret Word

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_SECRET_WORD[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_SECRET_WORD~	RET	spell_res	END

COPY_EXISTING	~RAMYSTF.spl~	override
LPF ADD_SPELL_EFFECT INT_VAR header = 3 opcode = 206 target = 2 power = 0 resist_dispel = 0 timing = 9 STR_VAR resource = EVAL ~%spell_res%~ END

END

//Immunity 3: Breach

ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]WIZARD_BREACH[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~WIZARD_BREACH~	RET	spell_res	END

COPY_EXISTING	~RAMYSTF.spl~	override
LPF ADD_SPELL_EFFECT INT_VAR header = 3 opcode = 206 target = 2 power = 0 resist_dispel = 0 timing = 9 STR_VAR resource = EVAL ~%spell_res%~ END

END

//Dispel Magic
		  
ACTION_IF	FILE_CONTAINS_EVALUATED	(~SPELL.IDS~	~[ %TAB%]CLERIC_DISPEL_MAGIC[ %TAB%%WNL%%LNL%%MNL%$]~)	BEGIN
LAF 	RES_NUM_OF_SPELL_NAME STR_VAR	spell_name = ~CLERIC_DISPEL_MAGIC~	RET	spell_res	END

COPY_EXISTING	~%spell_res%.spl~	~override/RAMYST1.spl~
LPM ~SPELL_TO_INNATE~

END

//Multiclass CLAB

COPY_EXISTING ~RAMyst.2da~ ~override/RAMystCM.2da~
SET_2DA_ENTRY 3 25 50 ~AP_RAMMYST~
SET_2DA_ENTRY 3  1 50 ~AP_RASLOT~
PRETTY_PRINT_2DA

//Restriction

COPY_EXISTING_REGEXP ~.*\.itm~ override
	READ_BYTE 0x31 proficiency
	PATCH_MATCH proficiency WITH 
		89 90 91 92 93 94 95 96 98 99 103 104 105 106  BEGIN
		  LPF ADD_ITEM_EQEFFECT
			INT_VAR
			  opcode = 319
			  target = 1
			  parameter1 = (RAMyst + 0x4000)
			  parameter2 = 9
			  timing = 2
		  END
		END
		DEFAULT
	END
BUT_ONLY

//BAM

ACTION_FOR_EACH file IN ~MYSTS~ ~MYSTL~ BEGIN
  COPY ~DeitiesOfFaerun/Itm/HolySymbols/BAM/%file%.bam~ override
    LPF UPDATE_PVRZ_INDICES
    RET
      original_base_index
      new_base_index
    END

  ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
    LAF INSTALL_PVRZ
    INT_VAR
      original_base_index
      new_base_index
    STR_VAR
      source_file = EVAL ~DeitiesOfFaerun/Itm/HolySymbols/BAM/MOS%original_base_index%.pvrz~
    END
  END
END

//EoF