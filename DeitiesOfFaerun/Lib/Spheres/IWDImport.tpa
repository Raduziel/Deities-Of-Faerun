//Hold Person or Mammal

COPY_EXISTING ~SPPR305.spl~	override
	LPF	CLONE_EFFECT
		INT_VAR
			match_opcode = 175	
			match_parameter2 = 3
			match_parameter1 = 2
			parameter1 = 1
			silent = 1
	END
	LPF	CLONE_EFFECT
		INT_VAR
			match_opcode = 177
			match_parameter2 = 3
			match_parameter1 = 2
			parameter1 = 1
			silent = 1
	END
	LPF	ALTER_EFFECT
		INT_VAR
			match_opcode = 324
			match_parameter2 = 8
			parameter2 = 22
			silent = 1
	END
	LPF	ALTER_EFFECT
		INT_VAR
			match_opcode = 318
			match_parameter2 = 8
			parameter2 = 22
			silent = 1
	END
	
COPY_EXISTING ~IDPRO249.pro~ override
	WRITE_SHORT	0x3e	22
IF_EXISTS

//Curse

ACTION_IF !FILE_CONTAINS_EVALUATED(~SPELL.IDS~ ~CLERIC_CURSE~) BEGIN

ACTION_IF !FILE_EXISTS ~IDPRO237.PRO~ BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Curse/IDPRO237.PRO~

END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Curse/Files~ override

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/Curse/SPPR112.spl~ 1 1 CLERIC_CURSE
SAY NAME1 @999945 SAY NAME2 @999945 SAY UNIDENTIFIED_DESC @999946 SAY DESC @999946
LPF RES_NAME_OF_SPELL_NUM	INT_VAR spell_num = idsName RET spell_res	END
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END
LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO237% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 14406 parameter1 = EVAL "%RA_Cursed%" END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Curse/SPPR112B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Curse/SPPR112C.BAM~	~override\%spell_res%C.BAM~

END

//Prayer

ACTION_IF !FILE_CONTAINS_EVALUATED(~SPELL.IDS~ ~CLERIC_PRAYER~) BEGIN

ACTION_IF !FILE_EXISTS ~OHVRNP.PRO~ BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/OHVRNP.PRO~

END

ACTION_IF !FILE_EXISTS ~OHVRPO.PRO~ BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/OHVRPO.PRO~

END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/Files~ override

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316.spl~ 1 3 CLERIC_PRAYER
SAY NAME1 @999947 SAY NAME2 @999947 SAY UNIDENTIFIED_DESC @999948 SAY DESC @999948
LPF RES_NAME_OF_SPELL_NUM	INT_VAR spell_num = idsName RET spell_res	END
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/SPPR316C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/#RECITEB.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRNP% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40257 parameter1 = EVAL "%RA_Penalty%" END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@999947) STR_VAR bam_file = RAALLIC RET iconpray = icon END
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Prayer/#RECITEG.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRPO% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40256 parameter1 = EVAL "%RA_Bonus%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 183   parameter2 = iconpray END

END

//Recitation

ACTION_IF !FILE_CONTAINS_EVALUATED(~SPELL.IDS~ ~CLERIC_RECITATION~) BEGIN

ACTION_IF !FILE_EXISTS ~OHVRNP.PRO~ BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/OHVRNP.PRO~

END

ACTION_IF !FILE_EXISTS ~OHVRPO.PRO~ BEGIN

ADD_PROJECTILE ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/OHVRPO.PRO~

END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/Files~ override

ADD_SPELL ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/SPPR421.spl~ 1 4 CLERIC_RECITATION
SAY NAME1 @999949 SAY NAME2 @999949 SAY UNIDENTIFIED_DESC @999950 SAY DESC @999950
LPF RES_NAME_OF_SPELL_NUM	INT_VAR spell_num = idsName RET spell_res	END
WRITE_ASCIIE	0x3a ~%spell_res%C~	#8
LPF	ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%spell_res%B~	END

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/SPPR421B.BAM~	~override\%spell_res%B.BAM~
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/SPPR421C.BAM~	~override\%spell_res%C.BAM~

COPY ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/#RECITEB.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRNP% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40257 parameter1 = EVAL "%RA_Penalty%" END

LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@999949) STR_VAR bam_file = RAALLIC RET iconreci = icon END
COPY ~DeitiesofFaerun/Lib/Spheres/Import/Recitation/#RECITEG.SPL~ override
LPF ALTER_SPELL_HEADER INT_VAR projectile = %OHVRPO% END
LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 40256 parameter1 = EVAL "%RA_Bonus%" END
LPF ALTER_EFFECT INT_VAR match_opcode = 142 match_parameter2 = 175   parameter2 = iconreci END

END



//EoF